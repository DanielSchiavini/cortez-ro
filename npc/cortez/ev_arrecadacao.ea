//Arrecadação de zeny

new_zone01,52,136,4	script	Arrecadar	58,{
	mes "^FF0000[ Arrecadações ]^000066";
	if(getgmlevel()>60){
		if($arz_total){
			mes "Estamos tentando arrecadar " + $arz_total + "kk para criar " + $arz_mvps + " MVPs até às 19h.";
			mes "Até agora já arrecadamos " + $arz_pago + "kk.";
			if(select("OK:Testar") == 2){
				close2;
				donpcevent "Arrecadações::OnEventTime";
			}
		}else{
			mes "Deseja ativar o evento?";
			if(select("Não:Sim") == 2){
				set $arz_pago, 0;
				set $arz_aberto, 0;
				next;
				
				mes "^FF0000[ Arrecadações ]^000066";
				mes "Quantos milhões deseja arrecadar?";
				mes "(Digite um número entre 20 e 5000)";
				input $arz_total, 200 - 180 * ($serv==6), 5000;
				next;
				
				mes "^FF0000[ Arrecadações ]^000066";
				mes "Quantos MVPs deseja criar às 19h, se o zeny for arrecadado?";
				mes "(Digite um número entre 2 e 20)";
				mes "Os mini-bosses também serão criados baseando-se neste número.";
				input $arz_mvps, 2, 20;
				next;
				
				mes "^FF0000[ Arrecadações ]^000066";
				mes "Deseja confirmar o evento, que criará " + $arz_mvps + " MVPs se os jogadores doarem " + $arz_total + "kk?";
				if(select("Não:Sim") == 1){
					set $arz_total, 0;
					set $arz_mvps, 0;
				}else{
					logmes "[Arrecadações] Iniciado, " + $arz_total + "kk, " + $arz_mvps + " MVPs.";
					enablenpc "Arrecadações";
					close2;
					sleep 3000;
					while($arz_total > 0){
						if($arz_pago >= $arz_total){
							announce "[Arrecadações] Oi gente, conseguimos arrecadar o zeny necessário para o evento!", bc_all;
							sleep 3000;
							announce "[Arrecadações] Às 19h, criaremos diversos MVPs! Não perca essa!", bc_all;
							sleep 32*60000;
						}else{
							announce "[Arrecadações] Oi gente, estou arrecadando zeny para o servidor...", bc_all;
							sleep 3000;
							announce "[Arrecadações] Se conseguirmos juntar " + $arz_total + "kk até às 19h, diversos MVPs serão criados!", bc_all;
							sleep 17*60000;
						}
					}
				}
			}
		}
	}
	close;
}

brasilis,266,89,5	script	Arrecadações	58,{
	if($arz_aberto){
		mes "^FF0000[ Arrecadações ]^000066";
		mes "Deseja ir para o mapa do evento?";
		if(select("Não:Sim") == 2){
			close2;
			warp "alde_gld",280,160;
			end;
		}
	}else if($arz_total){
		mes "^FF0000[ Arrecadações ]^000066";
		mes "Olá!";
		mes "Estamos arrecadando zeny para organizar um evento no servidor.";
		mes "Se juntarmos pelo menos " + $arz_total + "kk, o evento será realizado.";
		mes "Já juntamos " + $arz_pago + "kk até agora.";
		next;
		
		mes "^FF0000[ Arrecadações ]^000066";
		mes "Deseja participar dando sua contribuição?";
		if(select("Não:Sim") == 2){
			mes "A quantia deve ser entre 1 e 100kk.";
			mes "Por favor, digite-a em ^FF0000milhões^000066.";
			set @arz_zeny, 0;
			while(@arz_zeny<1 || @arz_zeny>100)
				input @arz_zeny;
			
			next;
			mes "^FF0000[ Arrecadações ]^000066";
			if(callfunc("tem_milhoes", @arz_zeny)){
				mes "Deseja confirmar sua contribuição de " + @arz_zeny + "kk para a organização do evento?";
				if(select("Não:Sim") == 2){
					callfunc("tira_milhoes", @arz_zeny, "Arrecada");
					set $arz_pago, $arz_pago + @arz_zeny;
					next;
					
					mes "^FF0000[ Arrecadações ]^000066";
					mes "Agradecemos muito pela sua ajuda!";
					if(@arz_zeny > 15){
						announce "[Arrecadações] Obrigado " + strcharinfo(0) + " pela sua contribuição de " + @arz_zeny + "kk!", bc_all;
					}
					set @arz_zeny, 0;
				}
			}else{
				mes "^FF0000[ Arrecadações ]^000066";
				mes "Desculpe-me, mas acredito que você não tenha tanto zeny disponível!";
			}
		}
	}
	close;
	
	OnEventTime:
	OnClock1905:
		if($arz_total){
			callfunc("log_mes","Necessário: " + $arz_total + ", Pago: " + $arz_pago,"Arrecada","alde_gld");
			if($arz_pago >= $arz_total){
				announce "[Arrecadações] Conseguimos arrecadar o zeny suficiente para o evento!", bc_all;
				sleep 3000;
				announce "[Arrecadações] Estamos criando neste momento os MVPs! Fale comigo para participar!", bc_all;
				set $arz_total, 0;
				set $arz_pago, 0;
				set $arz_aberto, 1;
				
				callfunc "F_UseBloodyBranch",$arz_mvps,"alde_gld",0,0,"Arrecadações::OnMvpDead",300,300;
				callfunc "F_UseDeadBranch",$arz_mvps*3,"alde_gld",0,0,"Arrecadações::OnMobDead",300,300;
				callfunc "F_UseDeadBranch",$arz_mvps*3,"alde_gld",0,0,"Arrecadações::OnMobDead",300,300;
				
				areamonster "alde_gld",0,0,300,300,"Ghostring",1120,$arz_mvps/2,"Arrecadações::OnMiniBossDead";
				areamonster "alde_gld",0,0,300,300,"Archangeling",1388,$arz_mvps/2,"Arrecadações::OnMiniBossDead";
				areamonster "alde_gld",0,0,300,300,"Giant Deviling",3251,$arz_mvps/2,"Arrecadações::OnMiniBossDead";
				areamonster "alde_gld",0,0,300,300,"Angeling",1096,$arz_mvps/2,"Arrecadações::OnMiniBossDead";
				areamonster "alde_gld",0,0,300,300,"Deviling",1582,$arz_mvps/2,"Arrecadações::OnMiniBossDead";
				areamonster "alde_gld",0,0,300,300,"Giant Khalitzburg",3250,$arz_mvps/2,"Arrecadações::OnMiniBossDead";
				areamonster "alde_gld",0,0,300,300,"Cristal",3303,$arz_mvps/2,"Arrecadações::OnMiniBossDead";
			}else{
				announce "[Arrecadações] Não conseguimos arrecadar o dinheiro suficiente para o evento!", bc_all;
				announce "[Arrecadações] Por favor, continuem a contribuir com zeny para podermos chegar ao valor amanhã.", bc_all;
			}
		}
		end;
	
	OnMvpDead:
		if(!mobcount("alde_gld","Arrecadações::OnMvpDead")){
			announce "[Arrecadações] Todos os MVPs foram mortos! Obrigado pela ajuda no evento!", bc_all;
			disablenpc "Arrecadações";
			set $arz_aberto, 0;
			sleep 2000;
			killmonsterall "alde_gld", 1;
			mapwarp "alde_gld","brasilis",266,105;
		}
		end;
		
	OnMobDead:
	OnMiniBossDead:
		end;
	
	OnInit:
		if(!$arz_total){
			disablenpc "Arrecadações";
			set $arz_aberto, 0;
		}
		end;
}
