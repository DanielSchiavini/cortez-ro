force_1-3	mapflag	nosave	SavePoint
force_1-3	mapflag	nomemo
force_1-3	mapflag	nopenalty
force_1-3	mapflag	nosave
force_1-3	mapflag	restricted	4
force_1-3	mapflag	nodrop
force_1-3	mapflag	notrade
force_1-3	mapflag	nowarpto

new_zone01,49,127,6	script	Batalha de PVP	936,{
	if(getgmlevel()<10) end;
	mes "[^660066 Batalha PvP ^000000]";
	mes "Olá, você gostaria de ir para o mapa e configurar uma batalha PvP?";
	if(select("Não:Sim")==2){
		close2;
		warp "guild_vs2-1",48,51;
		end;
	}else{
		close;
	}
}

force_1-3,100,173,4	script	Batalha PvP	936,{
	if(getgmlevel()>=10){
		if(.BatalhaPVP>1){
			if(getmapusers("force_1-3")==2){
				dispbottom "Você foi retirado do mapa para que o vencedor consiga ganhar o evento.";
				warp "SavePoint",0,0;
				end;
			}else if(getmapusers("force_1-3")!=1){
				mes "[^660066 Batalha PvP ^000000]";
				mes "Deseja cancelar a batalha atual? Ainda há "+getmapusers("force_1-3")+" jogadores no mapa, incluindo você.";
				next;
				if(select("Não!:Sim, por favor")==1) close;
				
				mes "[^660066 Batalha PvP ^000000]";
				mes "Ok, evento cancelado...";
				close2;
			}else{
				dispbottom "Evento cancelado pois só há você no mapa.";
			}
			
			set .BatalhaPVP, 0;
			gvgoff "force_1-3";
			logmes "[Batalha] Evento cancelado";
			mapwarp "force_1-3","SavePoint",0,0;
			end;
		}else if(.BatalhaPVP==1){
			mes "[^660066 Batalha PvP ^000000]";
			mes "A batalha PVP começará em breve.";
			close;
		}
		
		mes "[^660066 Batalha PvP ^000000]";
		mes "Olá, você gostaria de iniciar uma batalha PvP?";
		menu "Sim",-,"Não",L_Cancelar;
		next;
		
		L_Preparar:
			if (getmapusers("force_1-3")>1){
				mes "[^660066 Batalha PvP ^000000]";
				mes "Por favor, tire todos os outros jogadores do mapa para começar.";
				close;
			}
			
			set .@Nr_Part, 0;
			while(.@Nr_Part < 1 || .@Nr_Part > 14){
				mes "[^660066 Batalha PvP ^000000]";
				mes "A batalha será entre 2 grupos de 1 a 14 jogadores cada.";
				mes "Quantos integrantes em cada grupo participarão da batalha?";
				next;
				input .@Nr_Part;
			}
			
			mes "[^660066 Batalha PvP ^000000]";
			mes "A batalha então será um ^FF0000"+.@Nr_Part+"x"+.@Nr_Part+"^000000?";
			menu "Sim",-,"Não",L_Preparar,"Cancelar Evento",L_Cancelar;
		
		// Grupo A
		L_Grupo1:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "Digite o nome de um jogador do grupo 1 (ou fim para cancelar).";
			input .@lider1$;
			if(.@lider1$=="fim") goto L_Cancelar;
			set .@party_id1, getcharid(1, .@lider1$); //Localizando a party
			if(!.@party_id1){
				mes "O grupo não foi localizado.";
				goto L_Grupo1;
			}
			getpartymember(.@party_id1,0);
			if($@partymembercount != .@Nr_Part){
				deletearray $@partymembername$[0],$@partymembercount;
				mes "O grupo ^FF0000"+getpartyname(.@party_id1)+"^000000 tem ^FF0000"+$@partymembercount+"^000000 jogadores!";
				goto L_Grupo1;
			}
			mes "Grupo ^FF0000"+getpartyname(.@party_id1)+"^000000 localizado com ^FF0000"+.@Nr_Part+"^000000 jogadores:";
			copyarray .@party_members1$[0],$@partymembername$[0],.@Nr_Part;
			deletearray $@partymembername$[0], .@Nr_Part;
			
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1)
				mes (.@i+1) + ". "+.@party_members1$[.@i];
		
		// Grupo 2
		L_Grupo2:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "Digite o nome de um jogador do grupo 2 (ou fim para cancelar).";
			input .@lider2$;
			if(.@lider2$=="fim") goto L_Cancelar;
			set .@party_id2, getcharid(1, .@lider2$); //Localizando a party
			if(!.@party_id2){
				mes "O grupo não foi localizado.";
				goto L_Grupo2;
			}
			if(.@party_id1 == .@party_id2){
				mes "Você escolheu duas vezes o mesmo grupo!";
				goto L_Grupo2;
			}
			getpartymember(.@party_id2,0);
			if($@partymembercount != .@Nr_Part){
				deletearray $@partymembername$[0],$@partymembercount;
				mes "O grupo ^FF0000"+getpartyname(.@party_id2)+"^000000 tem ^FF0000"+$@partymembercount+"^000000 jogadores!";
				goto L_Grupo2;
			}
			mes "Grupo ^FF0000"+getpartyname(.@party_id2)+"^000000 localizado com ^FF0000"+.@Nr_Part+"^000000 jogadores.";
			copyarray .@party_members2$[0],$@partymembername$[0],.@Nr_Part;
			deletearray $@partymembername$[0],.@Nr_Part;
			
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1)
				mes (.@i+1) + ". "+.@party_members2$[.@i];
		
		L_Iniciar:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "O evento irá começar!";
			menu "Continuar",-,"Cancelar",L_Cancelar;
			set .BatalhaPVP, 1;
			logmes "[Batalha] Evento iniciado entre dois grupos de "+.@Nr_Part+" jogadores";
			close2;
			
			//warp e desbuffs
			warpparty "force_1-3",15,180, .@party_id1;
			warpparty "force_1-3",185,180, .@party_id2;
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1){
				sc_end SC_ALL, getcharid(3,.@party_members1$[.@i]);
				sc_end SC_ALL, getcharid(3,.@party_members2$[.@i]);
			}
			
			sleep 6000; //Loading screen
			
			mapannounce "force_1-3","Bem vindos ao modo de Batalha!",bc_yellow;
			sleep 5000;
			
			mapannounce "force_1-3","Dentro da arena vocês não poderão abrir o armazém.",bc_yellow;
			sleep 10000;
			
			mapannounce "force_1-3","Por favor se preparem para o combate, vocês tem 1 minuto",bc_yellow;
			sleep 30000;
			
			mapannounce "force_1-3","Por favor se preparem para o combate, vocês tem 30 segundos",bc_yellow;
			sleep 20000;
			
			mapannounce "force_1-3","Por favor se preparem para o combate, vocês tem 10 segundos",bc_yellow;
			sleep 8000;

			mapannounce "force_1-3", "A batalha vai começar em 2 segundos! Boa sorte!", bc_yellow;
			sleep 2000;
			
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1) {
				//warpando grupos sem usar warpparty para garantir só Nr_Part por grupo
				warpchar "force_1-3",70,180, getcharid(0, .@party_members1$[.@i]);
				warpchar "force_1-3",130,180, getcharid(0, .@party_members2$[.@i]);
			}

			set .BatalhaPVP, 2;
			
			if (getmapusers("force_1-3")<1){
				gvgoff "force_1-3";
				removemapflag "force_1-3",mf_nocommand;
			}else{
				gvgon "force_1-3";
				setmapflag "force_1-3",mf_nocommand;
				mapannounce "force_1-3", "Modo GVG ativado... Que vença o melhor!", bc_yellow;
			}
			end;
		
		L_Cancelar:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "O evento foi cancelado!";
			close;
		
	}else{
		if(.BatalhaPVP==0){
			dispbottom "Batalha PvP: A batalha ainda não começou!";
		}else{
			if (getmapusers("force_1-3")>1){
				dispbottom "Batalha PvP: Só falo com campeões!!!";
			}else{
				dispbottom "Batalha PvP: Você foi o campeão do evento! Parabéns!!!";
				set .BatalhaPVP, 0;
				logmes "[Batalha] "+strcharinfo(0)+" do grupo "+getpartyname(getcharid(1))+" foi o campeão";
				gvgoff "force_1-3";
				removemapflag "force_1-3",mf_nocommand;
				warp "SavePoint",0,0;
			}
		}
		end;
	}
}

//premiação
//	itens muito bons (para torneio 1x1)
//vendas +7
//SGH +7
//angeling card
//ghostring card
//LKH+7
//skell +7
//Tiara Dourada +7
//15 galhos sangrentos
//500m
//
//	itens bons (para torneio 2x2 até 4x4)
//
//SGH
//Coturnos
//spiritus sancti
//7 galhos sangrentos
//100m
//
//	itens medianos (para torneios 5x5 acima)
//
//5 pergaminhos de clonagem escravo
//3 galhos sangrentos
//50m
//30 Dr. Peper
//30 Red Bulls
//Cinto de morrigane
//Gleipnir