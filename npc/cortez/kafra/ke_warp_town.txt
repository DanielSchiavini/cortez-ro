//===== eAthena Script ======================================= 
//= Kafra Express - Warping Town Module
//===== By: ================================================== 
//= Skotlex 
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= eAthena SVN R3424+, RO Episode 8+ (LightHalzen)
//===== Description: ========================================= 
//= Part of the Kafra Express Script Package.
//= Offers warp services to towns.
//===== Additional Comments: ================================= 
//= See config.txt for configuration.
//============================================================ 

-	script	keInit_warpTown	-1,{
OnInit:	//Load Config
	donpcevent "keConfig::OnLoadWarpTown";
	end;
}

function	script	F_keWarpTown	{

	function SF_check;
	function SF_warp;

	set @cost,callfunc("F_keCost",$@kewt_cost,$@kewt_discount);
	set @niflcost,callfunc("F_keCost",$@kewt_niflCost,$@kewd_discount);
	if (@cost > Zeny && $@kewt_free)
		set @cost, Zeny;
	do {
		if ($@kewt_showOnline) {
			set @kmenu, select(
				"- Voltar",
				"- Cortez (0z/"+(getmapusers("schg_cas03"))+" "+$@ked_users$+")",
				"- Alberta ("+@cost+"z/"+(getmapusers("alberta")+getmapusers("alberta_in"))+" "+$@ked_users$+")",
				"- Al De Baran ("+@cost+"z/"+(getmapusers("aldebaran")+getmapusers("aldeba_in"))+" "+$@ked_users$+")",
				"- Amatsu ("+@cost+"z/"+(getmapusers("amatsu")+getmapusers("ama_in01")+getmapusers("ama_in02"))
					+" "+$@ked_users$+")",
				"- Ayothaya ("+@cost+"z/"+(getmapusers("ayothaya")+getmapusers("ayo_in01")+getmapusers("ayo_in02"))
					+" "+$@ked_users$+")",
				"- Comodo ("+@cost+"z/"+(getmapusers("comodo")+getmapusers("cmd_in01")+getmapusers("cmd_in02"))
					+" "+$@ked_users$+")",
				"- Einbech ("+@cost+"z/"+(getmapusers("einbech"))+" "+$@ked_users$+")",
				"- Einbroch ("+@cost+"z/"+(getmapusers("einbroch")+getmapusers("ein_in01"))+" "+$@ked_users$+")",
				"- Geffen ("+@cost+"z/"+(getmapusers("geffen")+getmapusers("geffen_in")+getmapusers("gef_tower"))
					+" "+$@ked_users$+")",
				"- Gon Ryun ("+@cost+"z/"+(getmapusers("gonryun")+getmapusers("gon_in"))+" "+$@ked_users$+")",
				"- Hugel ("+@cost+"z/"+(getmapusers("hugel")+getmapusers("hu_in01"))+" "+$@ked_users$+")",
				"- Ilha Esquecida ("+@cost+"z/"+(getmapusers("nameless_i")+getmapusers("nameless_in")+getmapusers("nameless_n"))+" "+$@ked_users$+")",
				"- Izlude ("+@cost+"z/"+(getmapusers("izlude")+getmapusers("izlude_in"))+" "+$@ked_users$+")",
				"- Jawaii ("+@cost+"z/"+(getmapusers("jawaii")+getmapusers("jawaii_in"))+" "+$@ked_users$+")",
				"- LightHalzen ("+@cost+"z/"+(getmapusers("lighthalzen")+getmapusers("lhz_in01")
					+getmapusers("lhz_in02")+getmapusers("lhz_in03"))+" "+$@ked_users$+")",
				"- Lou Yang ("+@cost+"z/"+(getmapusers("louyang")+getmapusers("lou_in01")+getmapusers("lou_in02"))
					+" "+$@ked_users$+")",
				"- Lutie ("+@cost+"z/"+(getmapusers("xmas")+getmapusers("xmas_in"))+" "+$@ked_users$+")",
				"- Morroc ("+@cost+"z/"+(getmapusers("morocc")+getmapusers("morocc_in")+getmapusers("moc_castle"))
					+" "+$@ked_users$+")",
				"- Moscóvia ("+@cost+"z/"+(getmapusers("moscovia")+getmapusers("mosk_in")+getmapusers("mosk_ship"))+" "+$@ked_users$+")",
				"- Niflheim ("+@niflcost+"z/"+(getmapusers("niflheim")+getmapusers("nif_in"))+" "+$@ked_users$+")",
				"- Payon ("+@cost+"z/"+(getmapusers("payon")+getmapusers("payon_in01")+getmapusers("payon_in02"))
					+" "+$@ked_users$+")",
				"- Prontera (0z/"+(getmapusers("prontera")+getmapusers("prt_castle")
					+getmapusers("prt_church")+getmapusers("prt_in"))+" "+$@ked_users$+")",
				"- Rachel ("+@cost+"z/"+(getmapusers("rachel")+getmapusers("ra_in01"))+" "+$@ked_users$+")",
				"- Umbala ("+@cost+"z/"+(getmapusers("umbala")+getmapusers("um_in"))+" "+$@ked_users$+")",
				"- Veins ("+@cost+"z/"+(getmapusers("veins")+getmapusers("ve_in")+getmapusers("ve_in02"))+" "+$@ked_users$+")",
				"- Yuno ("+@cost+"z/"+(getmapusers("yuno")+getmapusers("yuno_in01")+getmapusers("yuno_in02")
					+getmapusers("yuno_in03")+getmapusers("yuno_in04")+getmapusers("yuno_in05"))+" "+$@ked_users$+")"
			);
		} else {
			set @kmenu, select(
				"- Voltar",
				"- Cortez (0z)",
				"- Alberta ("+@cost+"z)",
				"- Al De Baran ("+@cost+"z)",
				"- Amatsu ("+@cost+"z)",
				"- Ayothaya ("+@cost+"z)",
				"- Comodo ("+@cost+"z)",
				"- Einbech ("+@cost+"z)",
				"- Einbroch ("+@cost+"z)",
				"- Geffen ("+@cost+"z)",
				"- Gon Ryun ("+@cost+"z)",
				"- Hugel ("+@cost+"z)",
				"- Ilha Esquecida ("+@cost+"z)",
				"- Izlude ("+@cost+"z)",
				"- Jawaii ("+@cost+"z)",
				"- LightHalzen ("+@cost+"z)",
				"- Lou Yang ("+@cost+"z)",
				"- Lutie ("+@cost+"z)",
				"- Morroc ("+@cost+"z)",
				"- Moscóvia ("+@cost+"z)",
				"- Niflheim ("+@niflcost+"z)",
				"- Payon ("+@cost+"z)",
				"- Prontera (0z)",
				"- Rachel ("+@cost+"z)",
				"- Umbala ("+@cost+"z)",
				"- Veins ("+@cost+"z)",
				"- Yuno ("+@cost+"z)"
			);
		}
		switch (@kmenu) {
		case 2:		//Cortez
			SF_warp 0,0,e_yawn,"schg_cas03",48,48;
			break;
		case 3:		//Alberta
			if (SF_check(ketw_alberta, #ketw_alberta))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"alberta",117,56;
			break;
		case 4:		//Aldebaran
			if (SF_check(ketw_aldebaran, #ketw_aldebaran))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"aldebaran",139,124;
			break;
		case 5:		//Amatsu
			if (SF_check(ketw_amatsu, #ketw_amatsu))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"amatsu",197,88;
			break;
		case 6:		//Ayathoya
			if (SF_check(ketw_ayothaya, #ketw_ayothaya))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"ayothaya",202,174;
			break;
		case 7:		//Comodo
			if (SF_check(ketw_comodo, #ketw_comodo))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"comodo",189,150;
			break;
		case 8:		//Einbech
			if (SF_check(ketw_einbech, #ketw_einbech))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"einbech",165,127;
			break;
		case 9:		//Einbroch
			if (SF_check(ketw_einbroch, #ketw_einbroch))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"einbroch",64,199;
			break;
		case 10:	//Geffen
			if (SF_check(ketw_geffen, #ketw_geffen))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"geffen",120,66;
			break;
		case 11:	//GonRyun
			if (SF_check(ketw_gonryun, #ketw_gonryun))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"gonryun",160,180;
			break;
		case 12:	//Hugel
			if (SF_check(ketw_hugel, #ketw_hugel))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"hugel",96,145;
			break;
		case 13:	//Ilha Esquecida
			SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"nameless_i",256,216;
			break;
		case 14:	//Izlude
			if (SF_check(ketw_izlude, #ketw_izlude))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"izlude",127,97;
			break;
		case 15:	//Jawaii
			if (SF_check(ketw_jawaii, #ketw_jawaii))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"jawaii",214,223;
			break;
		case 16:	//LightHalzen
			if (SF_check(ketw_lighthalzen, #ketw_lighthalzen))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"lighthalzen",158,92;
			break;
		case 17:	//Louyang
			if (SF_check(ketw_louyang, #ketw_louyang))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"louyang",218,118;
			break;
		case 18:	//Lutie
			if (SF_check(ketw_lutie, #ketw_lutie))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"xmas",148,133;
			break;
		case 19:	//Morroc
			if (SF_check(ketw_morroc, #ketw_morroc))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"morocc",162,91;
			break;
		case 20:	//Moscóvia
			SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"moscovia",137,50;
			break;
		case 21:	//Niflheim - Liberado
			callfunc "F_keWarp",$@kewt_niflCost,$@kewd_discount,e_yawn,"niflheim",194,185;
			break;
		case 19000:	//Niflheim - Original
			if (SF_check(ketw_niflheim, #ketw_niflheim)) {
				if (nif_q_done != 1) {
					mes "...?";
					callfunc "F_keIntro", e_no, "Por alguma razão, não poderei te levar para Niflheim!";
				} else
					callfunc "F_keWarp",$@kewt_niflCost,$@kewd_discount,e_yawn,"niflheim",194,185;
			}
			break;
		case 22: //Payon
			if (SF_check(ketw_payon, #ketw_payon))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"payon",159,181;
		case 23: //Prontera
			if (SF_check(ketw_prontera, #ketw_prontera))
				SF_warp 0,$@kewt_discount,e_yawn,"prontera",156,175;
			break;
		case 24: //Rachel
			if (SF_check(ketw_rachel, #ketw_rachel))
				SF_warp 0,$@kewt_discount,e_yawn,"rachel",130,118;
			break;
		case 25: //Umbala
			if (SF_check(ketw_umbala, #ketw_umbala))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"umbala",88,153;
			break;
		case 26: //Veins
			if (SF_check(ketw_veins, #ketw_veins))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"veins",216,123;
			break;
		case 27: //Yuno
			if (SF_check(ketw_yuno, #ketw_yuno))
				SF_warp $@kewt_cost,$@kewt_discount,e_yawn,"yuno",158,77;
			break;
		}
	} while (@kmenu > 1);
	return;
	
//SubFunction SF_check(char-flag, account_flag)
//Checks if the character can warp to that town taking into consideration
//traveller's mode.
function SF_check {
	if ($@kewt_travel && getarg(0) == 0 && getarg(1) == 0) {
		callfunc "F_keIntro", e_sry, "Desculpe, mas você só pode ir para cidades que você já visitou antes.";
		return 0;
	}
	return 1;
}

//SubFunction SF_warp (int cost, int discount, int emotion, String map, int x, int y)
//Modded from F_keWarp to allow free warping to towns when not enough zeny.
function SF_warp {
	set @cost, getarg(0);
	if (@cost > Zeny && $@kewt_free)
		set @cost, Zeny;
	if (!(callfunc("F_keCharge",@cost,getarg(1),1))) {
		callfunc "F_keIntro", e_an, "Você não tem zeny suficiente...";
		return;
	}
	emotion getarg(2);
	warp getarg(3),getarg(4),getarg(5);
	end;
}

}

//Invoked when we want to add a map to the list of travelled-to towns
function	script	F_keAddTravelTown	{
	set @map$, getarg(0);
	if ($@kewt_travel > 1) {
		if (@map$ == "alberta" || ketw_alberta)
			set #ketw_alberta,1;
		if (@map$ == "aldebaran" || ketw_aldebaran)
			set #ketw_aldebaran,1;
		if (@map$ == "amatsu" || ketw_amatsu)
			set #ketw_amatsu,1;
		if (@map$ == "ayothaya" || ketw_ayothaya)
			set #ketw_ayothaya,1;
		if (@map$ == "comodo" || ketw_comodo)
			set #ketw_comodo,1;
		if (@map$ == "einbech" || ketw_einbech)
			set #ketw_einbech,1;
		if (@map$ == "einbroch" || ketw_einbroch)
			set #ketw_einbroch,1;
		if (@map$ == "geffen" || ketw_geffen)
			set #ketw_geffen,1;
		if (@map$ == "gonryun" || ketw_gonryun)
			set #ketw_gonryun,1;
		if (@map$ == "hugel" || ketw_hugel)
			set #ketw_hugel,1;
		if (@map$ == "izlude" || ketw_izlude)
			set #ketw_izlude,1;
		if (@map$ == "jawaii" || ketw_jawaii)
			set #ketw_jawaii,1;
		if (@map$ == "lighthalzen" || ketw_lighthalzen)
			set #ketw_lighthalzen,1;
		if (@map$ == "louyang" || ketw_louyang)
			set #ketw_louyang,1;
		if (@map$ == "xmas" || ketw_lutie)
			set #ketw_lutie,1;
		if (@map$ == "morocc" || ketw_morroc)
			set #ketw_morroc,1;
		if (@map$ == "niflheim" || ketw_niflheim)
			set #ketw_niflheim,1;
		if (@map$ == "payon" || ketw_payon)
			set #ketw_payon,1;
		if (@map$ == "prontera" || ketw_prontera)
			set #ketw_prontera,1;
		if (@map$ == "rachel" || ketw_rachel)
			set #ketw_rachel,1;
		if (@map$ == "umbala" || ketw_umbala)
			set #ketw_umbala,1;
		if (@map$ == "yuno" || ketw_yuno)
			set #ketw_yuno,1;
		if (@map$ == "veins" || ketw_veins)
			set #ketw_veins,1;
	} else {
		if (@map$ == "alberta")
			set ketw_alberta,1;
		if (@map$ == "aldebaran")
			set ketw_aldebaran,1;
		if (@map$ == "amatsu")
			set ketw_amatsu,1;
		if (@map$ == "ayothaya")
			set ketw_ayothaya,1;
		if (@map$ == "comodo")
			set ketw_comodo,1;
		if (@map$ == "einbech")
			set ketw_einbech,1;
		if (@map$ == "einbroch")
			set ketw_einbroch,1;
		if (@map$ == "geffen")
			set ketw_geffen,1;
		if (@map$ == "gonryun")
			set ketw_gonryun,1;
		if (@map$ == "hugel")
			set ketw_hugel,1;
		if (@map$ == "izlude")
			set ketw_izlude,1;
		if (@map$ == "jawaii")
			set ketw_jawaii,1;
		if (@map$ == "lighthalzen")
			set ketw_lighthalzen,1;
		if (@map$ == "louyang")
			set ketw_louyang,1;
		if (@map$ == "xmas")
			set ketw_lutie,1;
		if (@map$ == "morocc")
			set ketw_morroc,1;
		if (@map$ == "niflheim")
			set ketw_niflheim,1;
		if (@map$ == "payon")
			set ketw_payon,1;
		if (@map$ == "prontera")
			set ketw_prontera,1;
		if (@map$ == "rachel")
			set ketw_rachel,1;
		if (@map$ == "umbala")
			set ketw_umbala,1;
		if (@map$ == "yuno")
			set ketw_yuno,1;
		if (@map$ == "veins")
			set ketw_veins,1;
	}
	return;
}
