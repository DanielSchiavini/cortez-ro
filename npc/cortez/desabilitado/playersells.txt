//Create Table Sellings(id int primary key auto_increment, seller_id int, buyer_id int, item_id int, valor int, dt_fim datetime, pagou bit, recebeu bit);
//insert into sellings (seller_id,buyer_id,item_id,valor,dt_fim,pagou,recebeu) Values (2000011,2000000,2199,200,now(),0,0)


//Select * From rou.sellings Where Not Pagou And Not recebeu And dt_fim+interval 3 day<now() limit 10

gonryun,147,106,5	script	Wilbis	784,{
	mes "^993300[ " + .name$ + " ]^003399";
	mes "Olá viajante... Eu vivo pelo mundo fazendo leilões...";
	mes "Se quiser, eu posso vender um item seu, ou se preferir pode comprar algumas coisas que eu tenho.";
	next;
	if(getgmlevel()>=50)
		menu "Comprar",L_Comprar,"Vender",L_Vender,"Cancelar leilões",L_Cancelar,"Ver caloteiros",L_Calote,"Sair",L_Sair;
	else
		menu "Comprar",L_Comprar,"Vender",L_Vender,"Sair",L_Sair;
	
	L_Calote:
		cleararray @lista$[0],0,getarraysize(@lista$);
		query_sql "Select Concat(S.valor,'kk ',Day(S.dt_fim),'/',Month(S.dt_fim),' ',I.nome2,' - ',C.name) From Sellings S Join bd.item I on I.id=S.item_id Join `char` C On S.buyer_id=C.account_id Where !Pagou&&!recebeu&&dt_fim<now()-interval 3 day&&dt_fim>now()-interval 3 month Group By C.account_id Order By S.valor Desc", @lista$;
		mes "^993300[ " + .name$ + " ]^003399";
		if(getarraysize(@lista$)==0){
			mes "Não houve nenhum caloteiro nos últimos meses!";
		}else{
			mes "Aqui estão os caloteiros:";
			for(set @i,0; @i<getarraysize(@lista$); set @i,@i+1)
				mes @lista$[@i];
			mes "Cuidado ao punir um dos jogadores, afinal eles provavelmente usaram contas falsas.";
		}
		close;
		
	L_Cancelar:
		cleararray @lista$[0],0,getarraysize(@lista$);
		query_sql "Select S.id,Concat(S.valor,'kk - ',I.nome2) From sellings S join bd.item I on I.id=S.item_id Where dt_fim>now() and not pagou and IfNull(buyer_id,0)=0 Order By S.valor", @id, @lista$;
		mes "^993300[ " + .name$ + " ]^003399";
		if(getarraysize(@lista$)==0){
			mes "Não há nenhum item sem ofertas sendo vendido!";
		}else{
			mes "Aqui estão os itens sem oferta:";
			for(set @i,0; @i<getarraysize(@lista$); set @i,@i+1)
				mes @lista$[@i];
			next;
			menu "Sair",L_Sair,"Cancelar um deles",-;
			
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Escolha na lista qual deles você quer cancelar...";
			set @menu$, @lista$[0];
			for(set @i, 1; @i < getarraysize(@lista$); set @i,@i+1)
				set @menu$, @menu$ + ":" + @lista$[@i];
			next;
			set @menu, select(@menu$)-1;
			set @id,@id[@menu];
			set @menu$,@lista$[@menu];
			set @menu, 0; deletearray @lista$[0],getarraysize(@lista$);
			
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Deseja cancelar o item "+@menu$+"?";
			menu "Não",L_Sair,"Sim, certeza",-;
			
			next;
			logmes "[Wilbis] "+@id+" Leilão finalizado";
			query_sql "Update sellings Set dt_fim=now() Where id=" + @id;
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Pronto! A venda do item foi cancelada!";
		}
		close;
		
	L_Comprar:
		mes "^993300[ " + .name$ + " ]^003399";
		cleararray @item$[0],0,getarraysize(@item$);
		query_sql "Select IfNull(Max(I.nome2),'') From sellings S join bd.item I on I.id=S.item_id Where not pagou and buyer_id="+getcharid(3), @item$;
		if(@item$!=""){
			query_sql "Select S.id,valor,if(dt_fim<now(),1,0),item_id From sellings S join bd.item I on I.id=S.item_id Where not pagou and buyer_id=" + getcharid(3) + " and I.nome2='" +@item$+ "'", @id, @valor, @acabou, @item_id;
			if(@acabou==1){
				mes "Parabéns, você ganhou a compra do item " + @item$ + ".";
				mes "Agora, é só pagar os " + @valor + "kk que receberá seu item.";
				next;
				mes "^993300[ " + .name$ + " ]^003399";
				if(!callfunc("tem_milhoes",@valor)){
					mes "Trate de arranjar os " + @valor + "kk rápido, pois se o item não for retirado rapidamente, você pode ser punido pela equipe de GMs";
					next;
					next;
				}else{
					mes "Parabéns pela aquisição!";
					callfunc("tira_milhoes",@valor,"wilbis");
					logmes "[Wilbis] "+@id+" Comprador pagou ("+@valor+"kk)";
					query_sql "Update sellings Set pagou=1 Where id=" + @id;
					mes "Obrigado pela utilização de meus serviços";
					getitem @item_id,1;
				}
			}else{
				mes "Você atualmente está participando da venda do item " + @item$ + ", e ofereceu " + @valor + "kk pelo item.";
				mes "Só é permitido tentar comprar um item por vez.";
			}
		}else{
			cleararray @lista$[0],0,getarraysize(@lista$);
			query_sql "Select Concat(S.valor,'kk - ',I.nome2) From sellings S join bd.item I on I.id=S.item_id Where dt_fim>now() and not pagou and seller_id!="+getcharid(3)+" Order By S.valor", @lista$;
			if(@lista$=="" || @lista$=="0"){
				mes "Por enquanto, não há nenhum item sendo vendido!";
			}else{
				mes "Aqui estão os itens disponíveis:";
				for(set @i,0; @i<getarraysize(@lista$); set @i,@i+1){
					if(@lista$[@i]=="" || @lista$[@i]=="0") break;
					mes @lista$[@i];
				}
				next;
				menu "Não gostei de nada",L_Sair,"Eu quero comprar um deles",-;
				
				mes "^993300[ " + .name$ + " ]^003399";
				mes "Escolha na lista qual deles você gostou...";
				set @menu$, @lista$[0];
				for(set @i, 1; @i < getarraysize(@lista$); set @i,@i+1){
					if(@lista$[@i]=="" || @lista$[@i]=="0") break;
					set @menu$, @menu$ + ":" + @lista$[@i];
				}
				next;
				set @menu$,@lista$[(select(@menu$))-1];
				
				cleararray @item$[0],0,getarraysize(@item$);
				query_sql "Select S.id,valor,I.nome2 From sellings S join bd.item I on I.id=S.item_id Where dt_fim>now() and not pagou and Concat(S.valor,'kk - ',I.nome2)='" +@menu$+ "'", @id, @valor, @item$;
				mes "^993300[ " + .name$ + " ]^003399";
				mes "O lance mínimo deve ser igual ao valor do lance anterior adicionando 10%... ("+(@valor + (@valor/10) + 1)+" ou mais milhões)";
				next;
				mes "^993300[ " + .name$ + " ]^003399";
				mes "Qual o valor do lance ^990000(EM MILHÕES DE Zeny)^003399 que deseja dar?";
				mes "^990000LEMBRE-SE:^003399 Se você vencer e não retirar o item em 3 dias perderá o item E o dinheiro!";
				input @lance;
				next;
				
				mes "^993300[ " + .name$ + " ]^003399";
				if(@lance <= @valor + @valor/10){
					mes "O lance mínimo deve ser igual ao valor do lance anterior adicionando 10%... ("+(@valor + (@valor/10) + 1)+" ou mais milhões)";
				}else if(!callfunc("tem_milhoes",@lance)){
					mes "Você precisa ter o dinheiro pra pagar o seu lance de "+@lance+" milhões!";
				}else if(@item$!=""){
					mes "Deseja confirmar seu lance de "+@lance+" milhões de Zeny pelo item "+@item$+"?";
					next;
					menu "Cancelar",L_Sair,"Confirmar",-;
					
					query_sql "Update sellings set buyer_id="+getcharid(3)+",valor="+@lance+" Where id="+@id+" and valor<"+@lance;
					logmes "[Wilbis] "+@id+" Lance "+@lance+"kk";
					
					set .@msg$, "Wilbis: Foi oferecido um lance de "+@lance+" milhões de Zeny pelo item "+@item$+"!";
					mapannounce "gonryun",	.@msg$, bc_map, .color;	//cidade principal
					mapannounce "pvp_y_1-2",.@msg$, bc_map, .color;	//cidade do comércio
					mes "^993300[ " + .name$ + " ]^003399";
					mes "Obrigado, seu lance de "+@lance+" milhões de Zeny foi registrado. Boa sorte!";
					mapannounce "pvp_y_1-2","Foi oferecido o lance de "+@lance+" milhões para o item "+@item$, bc_map;
				}
			}
		}
		close;
	
	L_Vender:
		mes "^993300[ " + .name$ + " ]^003399";
		set @id,0;
		query_sql "Select S.id,item_id,I.nome2,if(pagou,1,0),valor,ifNull(buyer_id,0),if(dt_fim<now(),1,0),ch.name,date_format(S.dt_fim,'%d/%m/%Y às %H:%i:%s') From sellings S join bd.item I on I.id=S.item_id left join `char` ch on ch.account_id=S.buyer_id Where not recebeu and seller_id="+getcharid(3)+" Order by ch.zeny desc Limit 1", @id, @item_id, @item$, @pagou, @valor, @buyer_id, @acabou, @buyer$, @dt_fim$;
		if(@id){
			if(@pagou==1){
				mes "Você já vendeu o item " + @item$ + " para o jogador '" + @buyer$ + "'...";
				mes "Como ele já pagou, agora pode receber o equivalente à " + @valor + "kk menos os " + .comissao + "% de comissão";
				set @valor, ((@valor*(100-.comissao))/100);
				logmes "[Wilbis] "+@id+" Recebido o valor de "+@valor+"kk";
				query_sql "Update sellings Set recebeu=1 Where id="+@id;
				callfunc("dar_milhoes",@valor,"wilbis");
				next;
				mes "^993300[ " + .name$ + " ]^003399";
				mes "Parabéns pela sua venda, e obrigado pela comissão :)";
			}else{
				if(@acabou==1){
					if(@buyer_id>0){
						mes "O seu item " + @item$ + " já foi vendido!";
						mes "O comprador foi '" + @buyer$ + "' pelo valor de " + @valor + "kk";
						mes "Foi finalizado em " + @dt_fim$ + ", mas o comprador ainda não pagou pelo prêmio.";
						next;
						mes "^993300[ " + .name$ + " ]^003399";
						mes "Se o comprador demorar mais de 3 dias, fale com um GM para que o mesmo seja punido.";
						mes "Não esqueça de avisar o ganhador, pois às vezes ele pode não saber que ganhou!";
						mes "Obrigado pela utilização de meus serviços.";
					}else{
						mes "O seu item " + @item$ + " não foi vendido.";
						mes "Nenhum jogador fez lances... E você terá que pagar minha comissão de " + .comissao + "% para reaver o item.";
						mes "O valor do item era " + @valor + "kk";
						set @valor,((@valor*1000*.comissao)/100)*1000;
						mes "Minha comissão fica em "+(@valor/1000)+"k zeny.";
						next;
						menu "Pagar a comissão",-,"Desistir",L_Sair;
						
						mes "^993300[ " + .name$ + " ]^003399";
						if(Zeny>=@valor){
							query_logsql "INSERT INTO zenylog(time,char_id,src_id,type,amount,map)VALUES(now(),"+getcharid(0)+","+getcharid(0)+",'N',-"+@valor+",'wilbis')";
							set Zeny,Zeny-@valor;
							logmes "[Wilbis] "+@id+" Vendedor pagou a comissão de "+(@valor/1000)+"k para receber o item de volta";
							query_sql "Update sellings Set pagou=1,recebeu=1 Where id="+@id;
							mes "Obrigado pela utilização de meus serviços";
							getitem @item_id,1;
						}else{
							mes "Você não tem Zeny suficiente para minhas comissões.";
							mes "Volte quando tiver meus " + @valor + "z";
						}
					}
				}else{
					mes "O seu item " + @item$ + " está sendo vendido no momento.";
					if(@buyer_id>0)
						mes "O último lance foi no valor de " + @valor + "kk.";
					else
						mes "Ainda não foram feitos lances, o valor mínimo é " + @valor + "kk.";
					mes "Será finalizado em " + @dt_fim$ + ", mas é importante que os compradores não saibam desta data.";
				}
			}
		}else{
			mes "Para eu fazer uma venda para você, preciso receber um equipamento ou uma carta, além de um preço inicial.";
			next;
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Você pode escolher por quantos dias a venda irá durar, e durante este tempo, qualquer jogador poderá comprar seu item.";
			next;
			mes "^993300[ " + .name$ + " ]^003399";
			mes "No término do período, se ninguém ofertar nada, você precisará pagar " + .comissao + "% do valor inicial para receber o item de volta.";
			next;
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Se no entanto houverem ofertas, o autor da maior delas poderá pegar o item, e pagar para mim. Daí eu te pago, descontando minha comissão de " + .comissao + "%.";
			next;
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Se você quiser vender uma carta, equipe-a em um de seus equipamentos.";
			mes "Se for um equipamento, deixe-o sem carta nenhuma.";
			next;
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Deseja mesmo que eu faça uma venda para você?";
			next;
			menu "Não, obrigado",L_Sair,"Sim, eu quero vender",-;
			
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Legal! Qual dos seus itens eu devo ver?";
			next;
			set @equip,select("Mudei de idéia...",getequipname(1),getequipname(2),getequipname(3),getequipname(4),getequipname(5),getequipname(6),getequipname(7),getequipname(8),getequipname(9),getequipname(10))-1;
			if(@equip==0) goto L_Sair;
			
			mes "^993300[ " + .name$ + " ]^003399";
			if(getequipcardcnt(@equip)>0){
				mes "Interessante, você quer vender uma das cartas equipadas... Qual delas?";
				set @menu$, getitemname(getequipcardid(@equip,0));
				for(set @i, 1; @i < getequipcardcnt(@equip); set @i,@i+1)
					set @menu$, @menu$ + ":" + getitemname(getequipcardid(@equip,@i));
				next;
				set @card,select(@menu$);
				set @item,getequipcardid(@equip,@card-1);
				mes "^993300[ " + .name$ + " ]^003399";
				if(@item<=4000) close;
			}else{
				set @card,0;
				set @item,getequipid(@equip);
			}
			if(@item<=0){
				mes "Não consegui achar seu item!";
				//logmes "[Erro] Não achei o item "+@item+" em "+@equip+" card "+@card+"!";
				close;
			}
			mes "Legal, irei então fazer a venda de " + getitemname(@item);
			next;
			
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Quantos dias você quer que a venda dure?";
			mes "Favor escolher um número de dias entre 5 e 15.";
			next;
			set @duracao,0;
			while(@duracao<5 || @duracao>15)
				input @duracao;
			
			/// Valores mínimos e máximos definidos aqui
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Qual será o valor mínimo, em milhões de zeny?";
			mes "Eu não vendo itens que valem menos de 20 milhões, e não aceito lances iniciais de mais de 800 milhões.";
			next;
			set @valor,0;
			while(@valor<20 || @valor>800)
				input @valor;
			
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Última chance de desistir... Deseja mesmo fazer esta venda de "+getitemname(@item)+" por "+@duracao+" dias e inicial de "+@valor+"kk?";
			next;
			menu "Não, desisto",L_Sair,"Sim, certeza",-;
			
			if(@card>0)
				successremovecards @equip;
			if(countitem(@item)<=0) close;
			query_sql "Insert Into Sellings (seller_id,item_id,valor,dt_fim,pagou,recebeu) Values ("+getcharid(3)+","+@item+","+@valor+",now()+interval "+@duracao+" day,0,0)";
			query_sql "Select max(id) From Sellings", @id; set @id, @id[0];
			logmes "[Wilbis] "+@id+" Item "+@item+" colocado à venda por "+@valor+"kk e "+@duracao+" dias";
			delitem @item,1;
			
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Prontinho, sua venda foi registrada e estará visível para todos os jogadores.";
			mes "Para verificar o status, basta falar comigo e escolher a opção de vender.";
			next;
			set .@msg$, "Wilbis: O item "+getitemname(@item)+" foi colocado à venda por "+@valor+" milhões de Zeny!";
			mapannounce "gonryun",	.@msg$,bc_map,.color;	//cidade principal
			mapannounce "pvp_y_1-2",.@msg$,bc_map,.color;	//cidade do comércio
			
			mes "^993300[ " + .name$ + " ]^003399";
			mes "Obrigado pela utilização dos meus serviços!";
			mapannounce "pvp_y_1-2","O item "+getitemname(@item)+" foi colocado à venda por "+@valor+" milhões", bc_map;
		}
		close;
	
	L_Sair:
		close;
	
	OnInit:
		initnpctimer;
		set $playerSellsId,0;
		set .name$,"Wilbis";
		set .color,0xFFCCCC;
		set .comissao, 2;	// em %
		
		while(1){
			setnpctimer 0;
			initnpctimer;
			end;
			
			OnTimer900000:
				query_sql "Select S.id,I.nome2,Concat(S.valor,'kk') From sellings S join bd.item I on I.id=S.item_id Where dt_fim>now() and not pagou and S.id>" + $playerSellsId + " Order By S.id Limit 1", .@playerSellsId, .@item$, .@valor$;
				set $playerSellsId,.@playerSellsId[0];
				if(.@item$!=""){
					set .@msg$, .name$ + ": Olá! Venda do item " + .@item$[0] + " por " + .@valor$[0] + ". Confira!";
					mapannounce "gonryun",	.@msg$,bc_map,.color;	//cidade principal
					mapannounce "pvp_y_1-2",.@msg$,bc_map,.color;	//cidade do comércio
				}else{
					set $playerSellsId,0;
				}
				stopnpctimer;
		}
	end;
}