//Por Brunim

//NPC Dia dos Namorados - Parte 1 (Boy)
prontera,147,174,5	script	Colegial Apaixonado	907,{
	if(gettime(7)!=2008 || gettime(6)!=6 || gettime(5)>15){ //Ano!=2008 OU Mês!=Junho OU Dia>24
		debugmes "DESABILITAR QUEST DO DIA DOS NAMORADOS!";
		disablenpc "Colegial Apaixonado";
		end;
	}else  if(gettime(5)<9){ //Antes do dia 9, somente para GMs
		if(getgmlevel()<50) end;
		set .@name$, "EM TESTES";
	}else{
		set .@name$, "Colegial Apaixonado";
	}
	
	mes	"[^660066 "+.@name$+" ^000000]";
	if(Sex==0 && getgmlevel()<50){
		mes "Hoje é um bom dia pra dar uma volta pela cidade com seu namorado, não acha?";
	}else{
		if(Sex==0){
			mes "Hoje é um bom dia pra dar uma volta pela cidade com seu namorado, não acha?";
			next;
			mes	"[^660066 "+.@name$+" ^000000]";
		}
		mes "Oi, cara! Tudo bem?";
		mes "Estou tentando fazer um presente bem legal pra minha namorada, mas não tenho tudo o que preciso. Você poderia me ajudar?";
		mes "Se você aceitar eu faço algo pra você presentear sua namorada também. OK?";
		menu "Tudo bem, vou te ajudar!",-,"A namorada é sua, pegue os itens sozinho!",L_Exit;
		next;
		
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Eu preciso dos seguintes itens:";
		mes "1x Laço Vermelho";
		mes "3x Rosa Eterna";
		mes "100x Cachecol Vermelho";
		mes "100x Coração Flamejante";
		menu "Beleza! Quando conseguí-los eu volto aqui.",L_Aguardar,"Tá na mão, cara!",-;
		next;
		
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Só um minuto...";
		next;
		mes	"[^660066 "+.@name$+" ^000000]";
		if (countitem(5083)!=1 || countitem(748)<3 || countitem(994)<100 || countitem(7216)<100) {
			mes "Você não pegou tudo o que eu pedi.";
			if(countitem(5083)>1)
				mes "E por favor, não venha me mostrar mais de um desses Laços Vermelhos!";
		}else{
			mes "Obrigado, amigo! Leve isto com você!";
			delitem 5083,1;
			delitem 748,3;
			delitem 994,100;
			delitem 7216,100;
			getitem 5140,1;
		}
	}
	close;
	
	L_Aguardar:
		next;
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Volte logo, hein?";
		close;

	
	L_Exit:
		next;
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Você quem sabe. Sua namorada ficará sem presente.";
		close;

}

//NPC Dia dos Namorados - Parte 2 (Girl)
prontera,164,162,3	script	Colegial Apaixonada	906,{
	if(gettime(7)!=2008 || gettime(6)!=6 || gettime(5)>15){ //Ano!=2008 OU Mês!=Junho OU Dia>24
		debugmes "DESABILITAR QUEST DO DIA DOS NAMORADOS!";
		disablenpc "Colegial Apaixonada";
		end;
	}else  if(gettime(5)<9){ //Antes do dia 9, somente para GMs
		if(getgmlevel()<50) end;
		set .@name$, "EM TESTES";
	}else{
		set .@name$, "Colegial Apaixonada";
	}
	
	mes	"[^660066 "+.@name$+" ^000000]";
	if(Sex==1 && getgmlevel()<50){
		mes "Meu namorado é muito ciumento. Não gosta de me ver falando com garotos...";
	}else{
		if(Sex==1){
			mes "Meu namorado é muito ciumento. Não gosta de me ver falando com garotos...";
			next;
			mes	"[^660066 "+.@name$+" ^000000]";
		}
		mes "Olá!!! Tudo bem?";
		mes "Estou me esforçando pra dar um presente ao meu namorado, você gostaria de me ajudar?";
		mes "Faremos lindos presentes se pudermos contar uma com a outra!";
		menu "Claro! Estava querendo uma mãozinha!",-,"Prefiro fazer tudo sozinha!",L_Exit;
		next;
		
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Por favor, junte isso pra mim";
		mes "1x Chapéu de Balde";
		mes "3x Rosa de Gelo";
		mes "100x Tinta de Polvo";
		mes "100x Coração Glacial";
		menu "OK! Logo estarei de volta!",L_Aguardar,"Já consegui o que faltava!",-;
		next;
		
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Oba!!!";
		next;
		mes	"[^660066 "+.@name$+" ^000000]";
		if (countitem(5120)!=1 || countitem(749)<3 || countitem(1024)<100 || countitem(7561)<100) {
			mes "Ah! Você me enganou... Não tem tudo ainda.";
			if(countitem(5120)>1)
				mes "E por favor, não venha me mostrar mais de um desses Chapéis de Balde!";
		}else{
			mes "Agora sim eles nos pedem em casamento!";
			delitem 5120,1;
			delitem 749,3;
			delitem 1024,100;
			delitem 7561,100;
			getitem 5184,1;
		}
	}
	close;
	
	L_Aguardar:
		next;
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Não demore, por favor!";
		close;
	
	L_Exit:
		next;
		mes	"[^660066 "+.@name$+" ^000000]";
		mes "Tudo bem. Seu namorado não ganhará presente.";
		close;
}