// NPC idealizado por Brunim, Chernevinho, Sola e Spok.
//
// Versão 1.0: NPC Criado, por Sola
// Versão 1.1: Correções, por Baltazar
// Versão 1.2: Verificação da data, por Baltazar
// Originalmente desenvolvido para: CortezRO.com.br
/////////////////////////////////////////////////

prontera,149,202,4	script	Coelhinho da Páscoa	1063,{
	if(gettime(7)!=2008 || gettime(6)!=3 || gettime(5)>24){ //Year!=2008 OR Month!=March OR Day>24
		disablenpc "Coelhinho da Páscoa";
		end;
	}
	if(getgmlevel()<59 && gettime(5)<17) end; //Before day 17th, only GMs are allowed to use the NPC
	
	//===== Premiação =====================================
	// Ovo de páscoa, ID 16751
	// Ovo de páscoa surpresa, ID 16752
	//===== Itens Necessários =========================
	// Ovo de páscoa: 519, 16759, 529, 536, 7111,  7166, 530
	// Quantidades:    20,   20,  10,  5,   10,    10,  10
	// Ovo de páscoa com surpresa: 519, 627, 16759, 7166, 2250, 12018, 12090, 682, 10010, 530, 7045
	// Quantidades:					30,   5,    40,   20,    1,     5,     1,   1,     5,   5,   15
	set @name$, "Coelhinho da Páscoa";
	mes "[^669900 "+@name$+" ^000000]";
	if(isequipped(2214)){
		mes "Olá, você gosta de ovos de chocolate?";
		menu "Sim, eu adoro!",L_Simpt1,"Argh, eu odeio chocolate!",L_Naopt1;
	}else{
		mes "Os coelhos têm medo de quem tem orelhas pequenas como a sua, sabia?";
		close;
	}

	L_Naopt1:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Ah, que pena! Vocês deveriam começar a gostar sabia?";
		close;
		
	L_Simpt1:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Pelo visto você é um dos meus clientes!";
		mes "Esse ano estou com novos produtos no mercado!";
		mes "Você está interessado?";
		next;
		menu "Com certeza!",-,"Não, obrigado",L_Exit;
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Tudo certo então!";
		mes "Você deseja uma pequena ou uma grande surpresa?";
		mes "Garanto que os dois valem a pena!";
		menu "Pequena por favor!",L_Pequena,"A maior possível!",L_Grande,"Obrigado, mas não gosto de chocolate",L_Naopt1;
	
	L_Pequena:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Para fazê-la, vou precisar de:";
		mes "- 20x Leite";
		mes "- 20x Cacau";
		mes "- 10x Bala";
		mes "- 5x Sorvete";
		mes "- 10x Pedaço de Papel";
		mes "- 10x Seda Macia";
		mes "- 10x Bengala Doce";
		menu "Ok, vou pegar os itens!",L_Esperando,"Aqui estão os itens",L_Pequena2;
	
	L_Pequena2:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Vamos ver se você tem tudo o que eu preciso...";
		next;
		mes "[^669900 "+@name$+" ^000000]";
		if (countitem(519)<20 || countitem(16759)<20 || countitem(529)<10 || countitem(536)<5 || countitem(7111)<10 || countitem(7166)<10 || countitem(530)<10) {
			mes "Desculpe, mas você não tem todos os itens que preciso!";
		}else{
			mes "Está com todo o necessário, vamos ver...";
			mes "Aqui está, uma surpresa pequena, como prometi!";
			delitem 519,20;
			delitem 16759,20;
			delitem 529,10;
			delitem 536,5;
			delitem 7111,10;
			delitem 7166,10;
			delitem 530,10;
			
			getitem 16751,1;
		}
		close;
	
	L_Grande:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "- 30x Leite";
		mes "- 5x Leite Doce";
		mes "- 40x Cacau";
		mes "- 20x Seda Macia";
		mes "- 1x Laço Meigo";
		mes "- 5x Fogo de Artifício";
		mes "- 1x Escorpião do Deserto no Vapor";
		mes "- 1x Vinho";
		mes "- 5x Panela Velha";
		mes "- 5x Bengala Doce";
		mes "- 15x tinta mágica";
		menu "Ok, vou pegar os itens!",L_Esperando,"Aqui estão os itens",L_Grande2;
	
	L_Grande2:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Vamos ver se você tem tudo o que eu preciso...";
		next;
		mes "[^669900 "+@name$+" ^000000]";
		if (countitem(519)<30 || countitem(627)<5 || countitem(16759)<40 || countitem(7166)<20 || countitem(2250)<1 || countitem(12018)<5 || countitem(12090)<1 || countitem(682)<1 || countitem(10010)<5 || countitem(530)<5 || countitem(7045)<15) {
			mes "Desculpe, mas você não tem todos os itens que preciso!";
		}else{
			mes "Está com todo o necessário, vamos ver...";
			mes "Aqui está, uma grande surpresa, como prometi!";
			delitem 519,30;
			delitem 627,5;
			delitem 16759,40;
			delitem 7166,20;
			delitem 2250,1;
			delitem 12018,5;
			delitem 12090,1;
			delitem 682,1;
			delitem 10010,5;
			delitem 530,5;
			delitem 7045,15;
			
			getitem 16752,1;
		}
		close;

	L_Esperando:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Estarei te esperando ansiosamente!";
		close;

	L_Exit:
		next;
		mes "[^669900 "+@name$+" ^000000]";
		mes "Ok.. Volte caso mude de idéia!";
		close;
}