arena_room	mapflag	nosave	SavePoint
prt_are_in	mapflag	nosave	SavePoint
guild_vs2-1	mapflag	nosave	SavePoint
guild_vs2-1	mapflag	nomemo
guild_vs2-1	mapflag	nopenalty
guild_vs2-1	mapflag	nosave
guild_vs2-1	mapflag	restricted	4
guild_vs2-1	mapflag	nodrop
guild_vs2-1	mapflag	notrade
guild_vs2-1	mapflag	nowarpto
guild_vs2-1	mapflag	nocommand	10

new_zone01,49,127,6	script	Batalha de PVP	936,{
	mes "[^660066 Batalha PvP ^000000]";
	mes "Olá, você gostaria de ir para o mapa e configurar uma batalha PvP?";
	if(select("Não:Sim")==2){
		close2;
		warp "guild_vs2-1",48,51;
		end;
	}else{
		close;
	}
}

guild_vs2-1,50,53,4	script	Batalha PvP	936,{
	if(getgmlevel()>=50){
		if(.BatalhaPVP>1){
			if(getmapusers("guild_vs2-1")==2){
				dispbottom "Você foi retirado do mapa para que o vencedor consiga ganhar o evento.";
				warp "SavePoint",0,0;
				end;
			}else if(getmapusers("guild_vs2-1")!=1){
				mes "[^660066 Batalha PvP ^000000]";
				mes "Deseja cancelar a batalha atual? Ainda há "+getmapusers("guild_vs2-1")+" jogadores no mapa, incluindo você.";
				next;
				if(select("Não!:Sim, por favor")==1) close;
				
				mes "[^660066 Batalha PvP ^000000]";
				mes "Ok, evento cancelado...";
				close2;
			}else{
				dispbottom "Evento cancelado pois só há você no mapa.";
			}
			
			set .BatalhaPVP, 0;
			gvgoff "guild_vs2-1";
			logmes "[Batalha] Evento cancelado";
			mapwarp "prt_are_in","gonryun",159,116;
			mapwarp "arena_room","gonryun",159,116;
			mapwarp "guild_vs2-1","gonryun",159,116;
			end;
		}else if(.BatalhaPVP==1){
			mes "[^660066 Batalha PvP ^000000]";
			mes "A batalha PVP começará em breve.";
			close;
		}
		
		L_Preparar:
			mes "[^660066 Batalha PvP ^000000]";
			mes "Olá, você gostaria de iniciar uma batalha PvP?";
			menu "Sim",-,"Não",L_Cancelar;
			next;
			
			set .@Nr_Part, 0;
			while(.@Nr_Part < 1 || .@Nr_Part > 14){
				mes "[^660066 Batalha PvP ^000000]";
				mes "A batalha será entre 2 grupos de 1 a 14 jogadores.";
				mes "Quantos integrantes em cada grupo participarão da batalha?";
				next;
				input .@Nr_Part;
			}
			
			mes "[^660066 Batalha PvP ^000000]";
			mes "A batalha será entre ^FF0000"+.@Nr_Part+"^000000 jogadores em cada grupo, correto?";
			menu "Sim",-,"Não",L_Preparar;
		
		// Grupo A
		L_Grupo1:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "Digite o nome de um jogador do grupo 1 (ou fim para cancelar).";
			input .@lider1$;
			if(.@lider1$=="fim") goto L_Cancelar;
			set .@party_id1, getcharid(1, .@lider1$); //Localizando a party
			if(!.@party_id1){
				mes "O grupo não foi localizado.";
				goto L_Grupo1;
			}
			getpartymember(.@party_id1,1);
			if($@partymembercount != .@Nr_Part){
				deletearray $@partymembercid[0],$@partymembercount;
				mes "O grupo ^FF0000"+getpartyname(.@party_id1)+"^000000 tem ^FF0000"+$@partymembercount+"^000000 jogadores!";
				goto L_Grupo1;
			}
			mes "Grupo ^FF0000"+getpartyname(.@party_id1)+"^000000 localizado com ^FF0000"+.@Nr_Part+"^000000 jogadores:";
			copyarray .@party_members1[0],$@partymembercid[0],.@Nr_Part;
			deletearray $@partymembercid[0], .@Nr_Part;
			
			getpartymember(.@party_id1,0);
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1)
				mes (.@i+1) + ". "+$@partymembername$[.@i];
			deletearray $@partymembername$[0],.@Nr_Part;
		
		// Grupo 2
		L_Grupo2:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "Digite o nome de um jogador do grupo 2 (ou fim para cancelar).";
			input .@lider2$;
			if(.@lider2$=="fim") goto L_Cancelar;
			set .@party_id2, getcharid(1, .@lider2$); //Localizando a party
			if(!.@party_id2){
				mes "O grupo não foi localizado.";
				goto L_Grupo2;
			}
			if(.@party_id1 == .@party_id2){
				mes "Você escolheu duas vezes o mesmo grupo!";
				goto L_Grupo2;
			}
			getpartymember(.@party_id2,1);
			if($@partymembercount != .@Nr_Part){
				deletearray $@partymembercid[0],$@partymembercount;
				mes "O grupo ^FF0000"+getpartyname(.@party_id2)+"^000000 tem ^FF0000"+$@partymembercount+"^000000 jogadores!";
				goto L_Grupo2;
			}
			mes "Grupo ^FF0000"+getpartyname(.@party_id2)+"^000000 localizado com ^FF0000"+.@Nr_Part+"^000000 jogadores.";
			copyarray .@party_members2[0],$@partymembercid[0],.@Nr_Part;
			deletearray $@partymembercid[0],.@Nr_Part;
			getpartymember(.@party_id2,0);
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1)
				mes (.@i+1) + ". "+$@partymembername$[.@i];
			deletearray $@partymembername$[0],.@Nr_Part;
		
		L_Iniciar:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "O evento irá começar! Após clicar em fechar, aguarde a confirmação.";
			menu "Continuar",-,"Cancelar",L_Cancelar;
			set .BatalhaPVP, 1;
			logmes "[Batalha] Evento iniciado entre dois grupos de "+.@Nr_Part+" jogadores";
			close2;
			dispbottom "Movendo jogadores e tirando buffs...";
			dispbottom "Não saia do jogo até receber a confirmação ou o evento será cancelado!";
			
			//warp e desbuffs
			set .@gm_id, getcharid(3);
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1){
				if(attachrid(.@party_members1[.@i]))
					sc_end SC_ALL, .@party_members1[.@i];
				else
					set .BatalhaPVP, 0;
				if(attachrid(.@party_members2[.@i]))
					sc_end SC_ALL;
				else
					set .BatalhaPVP, 0;
				if(!.BatalhaPVP)
					break;
			}
			if(!attachrid(.@gm_id)) end;
			if(!.BatalhaPVP){
				dispbottom "Um dos jogadores não pôde ser localizado. Evento cancelado.";
				set .BatalhaPVP, 0;
				end;
			}else{
				warpparty "prt_are_in",  22,  83, .@party_id1;
				warpparty "arena_room", 135, 133, .@party_id2;
				dispbottom "Evento confirmado!";
				warp "guild_vs2-1", 50, 50;
			}
			
			sleep 5000; //Loading screen
			
			mapannounce "prt_are_in","Vocês têm 40 segundos para se prepararem para a batalha!",bc_yellow;
			mapannounce "arena_room","Vocês têm 40 segundos para se prepararem para a batalha!",bc_yellow;
			sleep 2000; /// Tempo diminuído em 10x para testes
			
			mapannounce "prt_are_in","Vocês têm 20 segundos para se prepararem para a batalha!",bc_yellow;
			mapannounce "arena_room","Vocês têm 20 segundos para se prepararem para a batalha!",bc_yellow;
			sleep 1000; /// Tempo diminuído em 10x para testes
			
			mapannounce "prt_are_in","Vocês têm 10 segundos para se prepararem para a batalha!",bc_yellow;
			mapannounce "arena_room","Vocês têm 10 segundos para se prepararem para a batalha!",bc_yellow;
			sleep 1000; /// Tempo diminuído em 10x para testes
			
			for(set .@i, 0; .@i < .@Nr_Part; set .@i, .@i + 1) {
				//warpando grupos sem usar warpparty para garantir só Nr_Part por grupo
				set .@x, (.@i>=7);
				warpchar "guild_vs2-1", 10-.@x, 53-.@i+7*.@x, .@party_members1[.@i];
				warpchar "guild_vs2-1", 90-.@x, 53-.@i+7*.@x, .@party_members2[.@i];
			}
			sleep 5000; //Loading screen
			mapannounce "guild_vs2-1", "A batalha vai começar em 5 segundos! Boa sorte!", bc_yellow;
			sleep 5000;
			set .BatalhaPVP, 2;
			
			if (getmapusers("guild_vs2-1")<1){
				gvgoff "guild_vs2-1";
			}else{
				gvgon "guild_vs2-1";
				mapannounce "guild_vs2-1", "Modo GVG ativado... Que vença o melhor!", bc_yellow;
			}
			end;
		
		L_Cancelar:
			next;
			mes "[^660066 Batalha PvP ^000000]";
			mes "O evento foi cancelado!";
			close;
		
	}else{
		if(.BatalhaPVP==0){
			dispbottom "Batalha PvP: A batalha ainda não começou!";
		}else{
			if (getmapusers("guild_vs2-1")>1){
				dispbottom "Batalha PvP: Só falo com campeões!!!";
			}else{
				dispbottom "Batalha PvP: Você foi o campeão do evento! Parabéns!!!";
				set .BatalhaPVP, 0;
				logmes "[Batalha] "+strcharinfo(0)+" do grupo "+getpartyname(getcharid(1))+" foi o campeão";
				gvgoff "guild_vs2-1";
				warp "SavePoint",0,0;
			}
		}
		end;
	}
}

//premiação
//	itens muito bons (para torneio 1x1)
//vendas +7
//SGH +7
//angeling card
//ghostring card
//LKH+7
//skell +7
//Tiara Dourada +7
//15 galhos sangrentos
//500m
//
//	itens bons (para torneio 2x2 até 4x4)
//
//SGH
//Coturnos
//spiritus sancti
//7 galhos sangrentos
//100m
//
//	itens medianos (para torneios 5x5 acima)
//
//5 pergaminhos de clonagem escravo
//3 galhos sangrentos
//50m
//30 Dr. Peper
//30 Red Bulls
//Cinto de morrigane
//Gleipnir