// Human	+50 int	High Wizard
// Orc		+50 agi	Rogue
// Elf		+50 str	Sniper

//TODO:
// give guilds 30min before WOE
// custom clothes
// custom map
// custom treasure
// change leader
// orc face

new_zone01,52,147,0	script	Faction War	55,{
	callfunc("F_FactionWar", "Kriemhild (test)", "prtg_cas01", 99, 32, agitcheck());
	end;

	OnInit:
		//disabling old portals
		disablenpc "prtg01";
		disablenpc "gefg03";
		disablenpc "arug302";
		
		// humans, orcs, elfs
		setarray $factionItem[0], 2260, 2291, 2286;
		//setarray $factionClothes[0], 7, 9, 12;
		//setarray $factionMap$[0], "pvp_y_1-1", "pvp_n_1-4", "job_monk";
		//setarray $factionTreasure$[0], ;
		sleep 1000;
		if($serv==5)
			setarray $factions[0], 414524, 414525, 414526; // supreme
		else
			setarray $factions[0], 111, 112, 113; // testes
		
		end;
	
	OnAgitEnd:
	OnAgitEnd2: // 15min after WOE
		//sleep 15*60*1000;
		sleep 5000;
		
		// everybody comes back to the old guild
		for(set $@i,0; $@i<getarraysize($factions) ; set $@i, $@i+1 ){
			set $@faction_id, $factions[$@i];
			query_sql "Select char_id From `guild` Where guild_id = " + $@faction_id, $@leader_id;
			query_sql "Select account_id, char_id From `char` Where guild_id = " + $@faction_id, $@account_ids, $@char_ids;
			debugmes "Faction " + $@i + " = " + $@faction_id + " leader ("+$@leader_id+")";
			for(set $@j,0; $@j<getarraysize($@char_ids) ; set $@j, $@j+1 ){
				set $@account_id, $@account_ids[$@j];
				set $@char_id, $@char_ids[$@j];
				
				if($@leader_id != $@char_id){
					debugmes "Char " + $@j + " = " + $@char_id + " (" + $@account_id + ")";
					// either online or offline, the player has to leave the faction
					callfunc("log_mes","[FactionWar] Player " + $@char_id + " leaving faction " + $@faction_id,"[FactionWar]", "", $@account_id, $@char_id);
					leaveguild($@char_id, $@account_id, $@faction_id);
					
					// if the char is online, he'll come back to the old clan immediately
					if(attachrid($@account_id)){
						if(getcharid(0) == $@char_id){
							if(guild_id) {
								sleep2 2000;
								logmes "[FactionWar] Player " + $@char_id + " returning to guild " + guild_id;
								if(joinguild($@char_id, guild_id)){
									dispbottom "Bem vindo de volta ao seu clã!";
								}else{
									dispbottom "Não foi possível retornar ao seu clã!";
								}
								set guild_id, 0;
							}else{
								dispbottom "Você saiu da facção!";
							}
						}else{
							debugmes "Wrong char of the same account";
						}
						detachrid;
					}else{
						debugmes "Char offline";
					}
				}
			}
		}
		end;
	
	OnPCLoginEvent:
		// If the char wasn't online after the war, he'll come back to his clan after the war
		if(guild_id) {
			set .@char_id, getcharid(0);
			logmes "[FactionWar] Player " + .@char_id + " returning to guild " + guild_id + " after login";
			if(!joinguild(.@char_id, guild_id)){
				dispbottom "Não foi possível retornar ao seu clã original";
			}
			set guild_id, 0;
		}
		end;
	
	OnPCLogoutEvent:
		end;
		
	OnSun0100:
		// Once a week the factions will be reset, so new factions can be chosen
		query_sql("Update ro.login Set faction_id = null Where faction_id is not null");
		end;
}


prt_gld,129,65,0	script	Kriemhild#FactionWar	55,{
	callfunc("F_FactionWar", "Kriemhild", "prtg_cas01", 99, 32, agitcheck());
}

gef_fild13,308,244,0	script	Trapesac#FactionWar	55,{
	callfunc("F_FactionWar", "Trapesac", "gefg_cas02", 70, 143, agitcheck());
}

aru_gld,68,150,0	script	Horn#FactionWar	55,{
	callfunc("F_FactionWar", "Horn", "arug_cas03", 141, 45, agitcheck2());
}

// callfunc("F_FactionWar", "NOME DO CASTELO/NPC", "WARP TO MAP", X, Y, agit_active);
function	script	F_FactionWar	{
	if(!getarg(4) || ($castleMap1$!=getarg(1) && $castleMap2$!=getarg(1))){
		mes "["+getarg(0)+"]";
		mes "No Cortez nós temos o sistema de guerra entre facções.";
		mes "Este castelo somente se abrirá durante a guerra do emperium.";
		close;
	}
	
	set .@char_id, getcharid(0);
	set .@account_id, getcharid(3);
	set .@guild_id, getcharid(2);
	
	if(.@char_id == getguildmasterid(.@guild_id)){
		// Faction leader..
		for(set $@i,0; $@i<getarraysize($factions) ; set $@i, $@i+1 ) {
			if(.@guild_id == $factions[$@i]) {
				warp getarg(1), getarg(2), getarg(3);
				end;
			}
		}
		mes "["+getarg(0)+"]";
		mes "No Cortez nós temos o sistema de guerra entre facções.";
		mes "Você não pode participar de uma facção sendo o líder do clã.";
		next;
		
		mes "["+getarg(0)+"]";
		mes "Para participar com este personagem, será necessário quebrar o clã.";
		if(getgmlevel()>=5){
			mes "Você pode também usar @changegm se preferir.";
		}
		mes "^990000ATENÇÃO:^000000 Lembre-se do armazém do clã!!";
		close2;
		
		guildopenstorage();
	}else{
		query_sql("Select IfNull(faction_id,0) From ro.login Where account_id = " + .@account_id, .@faction_id);
		
		if(!.@faction_id){
			set .@faction_id, $factions[rand(0, getarraysize($factions[0])-1)];
			query_sql("Update ro.login Set faction_id = " + .@faction_id + " Where account_id = " + .@account_id);
		}
		
		if(.@guild_id != .@faction_id){
			logmes "[FactionWar] Leaving guild " + .@guild_id + " to faction " + .@faction_id;
			if(.@guild_id){
				mes "["+getarg(0)+"]";
				mes "Para participar da guerra entre facções, você precisa sair temporariamente do seu clã.";
				set guild_id, .@guild_id;
				leaveguild(.@char_id);
				
				// No guild_id if he was in a faction
				for(set $@i,0; $@i<getarraysize($factions) ; set $@i, $@i+1 )
					if(guild_id == $factions[$@i])
						set guild_id, 0;
				next;
			}
			if(joinguild(.@char_id, .@faction_id)){
				query_sql "Update `char` set guild_id = " + .@faction_id + " Where char_id=" + .@char_id;
				mes "["+getarg(0)+"]";
				mes "Bem vindo à facção dos " + getguildname(.@faction_id) + "!";
				dispbottom "Bem vindo à facção dos " + getguildname(.@faction_id) + "!";
				close2;
				
				//setlook 7,
				
			}else{
				dispbottom "Não foi possível te adicionar à facção " + getguildname(.@faction_id) + " (" + .@faction_id + ")";
			}
		}
		warp getarg(1), getarg(2), getarg(3);
	}
	end;
}