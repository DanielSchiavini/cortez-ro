//if(callfunc("tem_milhoes",@valor))
function	script	tem_milhoes	{
	set @varmilhoes, getarg(0); //valor em milhões
	if(@varmilhoes > Zeny/1000000 + countitem(9929) * 1000){
		//debugmes "Jogador não tem "+@varmilhoes+"kk, mas somente "+(Zeny/1000000 + countitem(9929) * 1000)+"kk";
		//dispbottom "Jogador não tem "+@varmilhoes+"kk, mas somente "+(Zeny/1000000 + countitem(9929) * 1000)+"kk";
		return 0;
	}else{
		//dispbottom "Você tem "+@varmilhoes+"kk, e um total de "+(Zeny/1000000 + countitem(9929) * 1000)+"kk";
		return 1;
	}
}

//callfunc("dar_milhoes",@valor,"npc_name");
function	script	dar_milhoes	{
	set @varmilhoes, getarg(0); //valor em milhões
	
	if(@varmilhoes > 1000){
		getitem 9929,@varmilhoes/1000;
		logmes "[Bilhao] Vale bilhão recebido ("+(@varmilhoes/1000)+"x)";
		set @varmilhoes, @varmilhoes%1000;
	}
	if(@varmilhoes!=0){
		set @varmilhoes, @varmilhoes*1000000;
		if(Zeny + @varmilhoes > 1000000000){
			query_logsql "INSERT INTO zenylog(time,char_id,src_id,type,amount,map)VALUES(now(),"+getcharid(0)+","+getcharid(0)+",'N',"+@varmilhoes+"-1000000000,'"+escape_sql(getarg(1,"dar_milhoes"))+"')";
			set @varmilhoes, Zeny - 1000000000 + @varmilhoes;
			set Zeny,@varmilhoes;
			getitem 9929,1;
			logmes "[Bilhao] Vale bilhão recebido";
		}else{
			query_logsql "INSERT INTO zenylog(time,char_id,src_id,type,amount,map)VALUES(now(),"+getcharid(0)+","+getcharid(0)+",'N',"+@varmilhoes+",'"+escape_sql(getarg(1,"dar_milhoes"))+"')";
			set Zeny,Zeny+@varmilhoes;
		}
	}
	set @varmilhoes,0;
	return 1;
}

//callfunc("tira_milhoes",@valor,"npc_name");
function	script	tira_milhoes	{
	set @varmilhoes, getarg(0); //valor em milhões
	//dispbottom "tirando "+@varmilhoes;

	if(@varmilhoes > 1000){
		if(@varmilhoes%1000==0 && Zeny>=1000000000 && countitem(9929)+1==@varmilhoes/1000) set @varmilhoes, @varmilhoes-1;
		delitem 9929,@varmilhoes/1000;
		logmes "[Bilhao] Vale bilhão apagado ("+(@varmilhoes/1000)+"x)";
		set @varmilhoes, @varmilhoes%1000;
	}
	if(@varmilhoes!=0){
		if(Zeny>=@varmilhoes*1000000){
			query_logsql "INSERT INTO zenylog(time,char_id,src_id,type,amount,map)VALUES(now(),"+getcharid(0)+","+getcharid(0)+",'N',-"+(@varmilhoes*1000000)+",'"+escape_sql(getarg(1,"tira_kks"))+"')";
			set Zeny, Zeny - @varmilhoes*1000000;
		}else{
			delitem 9929,1;
			logmes "[Bilhao] Vale bilhão apagado";
			query_logsql "INSERT INTO zenylog(time,char_id,src_id,type,amount,map)VALUES(now(),"+getcharid(0)+","+getcharid(0)+",'N',1000000000,'"+escape_sql(getarg(1,"tira_milhoes"))+"')";
			query_logsql "INSERT INTO zenylog(time,char_id,src_id,type,amount,map)VALUES(now(),"+getcharid(0)+","+getcharid(0)+",'N',-"+(@varmilhoes*1000000)+",'"+escape_sql(getarg(1,"tira_kks"))+"')";
			set @varmilhoes,@varmilhoes*1000000-Zeny;
			set Zeny,1000000000 - @varmilhoes;
		}
	}
	set @varmilhoes, 0;
	return 1;
}

//set @x$, callfunc("formata_milhoes",1000);
function	script	formata_milhoes	{
	set .@varmilhoes, getarg(0); //valor em milhões
	if(.@varmilhoes>1000){
		set .@return$, .@varmilhoes/1000;
		set .@varmilhoes, .@varmilhoes%1000;
		if(.@varmilhoes>0){
			set .@return$, .@return$ + "," + (.@varmilhoes<100?(.@varmilhoes<10?"00":"0"):"") + (.@varmilhoes%100==0?.@varmilhoes/100:(.@varmilhoes%10==0?.@varmilhoes/10:.@varmilhoes)) + "bi";
		}
	}else{
		set .@return$, .@varmilhoes+"mi";
	}
	set .@varmilhoes,0;
	return .@return$;
}

//callfunc("log_mes","mes","Char/NPC name","map","acc_id=1","char_id=150000");
function	script	log_mes	{
	query_logsql "INSERT INTO npclog (npc_date,account_id,char_id,char_name,map,mes) Values (now(),"+escape_sql(getarg(3,"1"))+","+escape_sql(getarg(4,"150000"))+",'"+escape_sql(getarg(1))+"','"+escape_sql(getarg(2))+"','"+escape_sql(getarg(0))+"')";
	return;
}

//callfunc("send_mail",0=dest_id,1=dest_name,2=send_id(0),3=send_name(Server),4=title,5=message,6=zeny,7=nameid,8=amount);
function	script	send_mail	{
	set .@zeny,getarg(6,0);
	set .@nameid, getarg(7,0);
	set .@amount, getarg(8,0);
	if(.@zeny>1000000000){
		if(.@nameid==0){
			set .@nameid,9929; //vale-bilhões
			set .@amount, .@zeny/1000000000; //quantos bilhões
			set .@zeny, .@zeny%1000000000; //resto em zeny
		}else{
			debugmes "function send_mail executada com zeny="+.@zeny+" e nameid="+.@zeny+". dest_id="+getarg(0);
			return 0;
		}
	}
	query_sql "INSERT INTO mail(send_name,send_id,dest_name,dest_id,title,message,time,status,zeny,nameid,amount,identify)VALUES('"+escape_sql(getarg(3,"Server"))+"',"+escape_sql(getarg(2,"0"))+",'"+escape_sql(getarg(1))+"',"+escape_sql(getarg(0))+",'"+escape_sql(getarg(4,"Mensagem"))+"',Replace('"+escape_sql(getarg(5,""))+"','\n','\n\r '),unix_timestamp(),0,"+.@zeny+","+.@nameid+","+.@amount+",1);";
	return 1;
}


//Function F_UseBloodyBranch (int number_of_branches, string map, int x1, int y1, string trigger, int x2=x1, int y2=y1)
function	script	F_UseBloodyBranch	{
	if(playerattached()) dispbottom "MVPs:";
	for(set .@cont, 0; .@cont<getarg(0); set .@cont, .@cont+1){
		set .@id, $MVPs[rand(1,getarraysize($MVPs))-1];
		areamonster getarg(1),getarg(2),getarg(3),getarg(5,getarg(2)),getarg(6,getarg(3)),"--ja--",.@id,1,getarg(4);
		if(playerattached()) dispbottom " " + (.@cont+1) + ". " + strmobinfo(2, .@id) + " (" + .@id + ")";
	}
	return .@id;
}

//Function F_UseDeadBranch (int number_of_branches, string map, int x, int y, string trigger, int x2=x1, int y2=y1)
function	script	F_UseDeadBranch	{
	for(set .@cont, 0; .@cont<getarg(0); set .@cont, .@cont+1){
		areamonster getarg(1),getarg(2),getarg(3),getarg(5,getarg(2)),getarg(6,getarg(3)),"--ja--",-1,1,getarg(4);
	}
	return getarg(0);
}

//Function F_Dispell (int char_id = 0)
function	script	F_Dispell	{
	set .@char, getarg(0,0);
	for(set .@status, 0; .@status<SC_SPL_MATK; set .@status, .@status+1){
		// copiado do dispell em skill.c
		switch(.@status){
			case SC_WEIGHT50:    case SC_WEIGHT90:    case SC_HALLUCINATION:
			//case SC_STRIPWEAPON: case SC_STRIPSHIELD: case SC_STRIPARMOR:
			//case SC_STRIPHELM:   case SC_CP_WEAPON:   case SC_CP_SHIELD:
			case SC_COMBO:     //case SC_CP_ARMOR:    case SC_CP_HELM:
			case SC_STRFOOD:     case SC_AGIFOOD:     case SC_VITFOOD:
			case SC_INTFOOD:     case SC_DEXFOOD:     case SC_LUKFOOD:
			case SC_HITFOOD:     case SC_FLEEFOOD:    case SC_BATKFOOD:
			case SC_WATKFOOD:    case SC_MATKFOOD:    case SC_DANCING:
			case SC_GUILDAURA:   case SC_EDP:         case SC_AUTOBERSERK:
			case SC_CARTBOOST:   case SC_MELTDOWN:    case SC_SAFETYWALL:
			case SC_SMA:         case SC_SPEEDUP0:    case SC_NOCHAT:
			case SC_ANKLE:       case SC_SPIDERWEB:   case SC_JAILED:
			case SC_ITEMBOOST:   case SC_EXPBOOST:    case SC_LIFEINSURANCE:
			case SC_BOSSMAPINFO: case SC_PNEUMA:      case SC_AUTOSPELL:
			case SC_INCHITRATE:  case SC_INCATKRATE:  case SC_NEN:
			case SC_READYSTORM:  case SC_READYDOWN:   case SC_READYTURN:
			case SC_READYCOUNTER:case SC_DODGE:       case SC_WARM:
			case SC_SPEEDUP1:    case SC_AUTOTRADE:   case 253: //SC_CRITICALWOUND
			case SC_JEXPBOOST:	 case SC_INVINCIBLE:  case SC_INVINCIBLEOFF:
			case SC_HELLPOWER:	 case SC_MANU_ATK:    case SC_MANU_DEF:
			case SC_SPL_ATK:	 case SC_SPL_DEF:	  case SC_MANU_MATK:
			case SC_SPL_MATK:
				continue;
			default:
				if(.@char) sc_end .@status, .@char; else sc_end .@status;
		}
	}
	skilleffect 289,1;
	//if(.@char) sc_end SC_ENDURE, .@char; else sc_end SC_ENDURE;
	set .@char, 0;
	return 1;
}
