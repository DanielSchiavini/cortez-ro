// Mapflags / Proibir comandos nos mapas
payg_cas01	mapflag	nopenalty
payg_cas01	mapflag	nomemo
payg_cas01	mapflag	monster_noteleport
payg_cas01	mapflag	nocommand	50
payg_cas01	mapflag	nosave	SavePoint

payg_cas02	mapflag	nopenalty
payg_cas02	mapflag	nomemo
payg_cas02	mapflag	monster_noteleport
payg_cas02	mapflag	nocommand	50
payg_cas02	mapflag	nosave	SavePoint

payg_cas03	mapflag	nopenalty
payg_cas03	mapflag	nomemo
payg_cas03	mapflag	monster_noteleport
payg_cas03	mapflag	nocommand	50
payg_cas03	mapflag	nosave	SavePoint

payg_cas04	mapflag	nopenalty
payg_cas04	mapflag	nomemo
payg_cas04	mapflag	monster_noteleport
payg_cas04	mapflag	nocommand	50
payg_cas04	mapflag	nosave	SavePoint

payg_cas05	mapflag	nopenalty
payg_cas05	mapflag	nomemo
payg_cas05	mapflag	monster_noteleport
payg_cas05	mapflag	nocommand	50
payg_cas05	mapflag	nosave	SavePoint

//Payon Guild Castles Map
pay_gld,121,238,0	warp	payg01	1,1,payg_cas01,214,48
payg_cas01,214,44,0	warp	payg01-1	1,1,pay_gld,121,233
pay_gld,291,116,0	warp	payg02	1,1,payg_cas02,272,57
payg_cas02,276,61,0	warp	payg02-1	1,1,pay_gld,295,116
pay_gld,321,293,0	warp	payg03	1,1,payg_cas03,226,26
payg_cas03,226,22,0	warp	payg03-1	1,1,pay_gld,317,293
pay_gld,140,156,0	warp	payg04	1,1,payg_cas04,252,271
payg_cas04,252,275,0	warp	payg04-1	1,1,pay_gld,140,160
pay_gld,204,269,0	warp	payg05	3,1,payg_cas05,62,225
payg_cas05,62,222,0	warp	payg05-1	5,1,pay_gld,204,266

brasilis,264,94,5	script	Invasão dos Castelos	47,{
	if($castelo==0){
		donpcevent "Evento dos Castelos::OnInit";
		end;
	}
	
	OnClick:
		mes "[^666699 Invasão dos Castelos ^000000]";
		mes "Deseja participar do evento?";
		menu "Mais informações",OnInfos,"Não, obrigado",-,"Sim, por favor",OnWarp;
		close;
	
	OnInfos:
		next;
		mes "[^666699 Invasão dos Castelos ^000000]";
		mes "Quatro castelos foram invadidos, e agora precisamos reconquistá-los.";
		next;
		mes "[^666699 Invasão dos Castelos ^000000]";
		mes "Para reconquistar um castelo, é preciso matar o chefe.";
		next;
		if($castelo>1 && $castelo<5){
			mes "[^666699 Invasão dos Castelos ^000000]";
			mes "Até agora, "+($castelo-1)+(($castelo==2)? " castelo já foi reconquistado" : " castelos foram reconquistados");
			next;
		}
		goto OnClick;
	
	OnWarp:
		close2;
		callfunc "EvCastelosWarp";
		end;
}

new_zone01,49,124,5	script	Evento dos Castelos	47,{
	if(getgmlevel()<59) close;
	mes "[^666699 Evento dos Castelos ^000000]";
	if($castelo>0){
		mes "O evento já está acontecendo!";
		mes "Os jogadores estão lutando no castelo "+$castelo+".";
		mes "Deseja ir para o mapa?";
		menu "Não, obrigado",-,"Sim, por favor",OnWarp,"Cancelar evento",OnCancelar;
		close;
	}else{
		mes "Deseja iniciar o evento de invasão nos castelos?";
		if(select("Não, obrigado:Sim, por favor")==1) close;
		close2;
	}
	OnIniciaEvento:
		//Desabilitando entradas dos castelos
		set $castelo, 1;
		set $evcity$, "pay";
		
		//Criando array com monstros do evento. O primeiro é o chefe default.
		set @mobqtde, 7;
		setarray .mobs[0],	1291, 1306, 1437, 1453, 1453, 1453, 1467, 1467, 1490;
		setarray @mobs1[0],	1389, 1059, 1115, 1157, 1159, 1492, 1511, 1658, 1688, 1734, 3250, 3251;
		setarray @mobs2[0],	1086, 1038, 1046, 1087, 1147, 1190, 1252, 1272, 1312, 1583, 3098, 3157;
		setarray @mobs3[0],	3104, 1039, 1150, 1251, 1373, 1418, 1518, 1623, 1685, 1719, 1779, 3108;
		setarray @mobs4[0],	1751, 1087, 1115, 1147, 1150, 1583, 1708, 3089, 3101, 3102, 3103, 3105;
		setarray @mobs5[0],	3302, 3303, 3303, 3303, 3303, 3303, 3303, 3303, 3303;	//Chefe é sempre o primeiro
		
		//Looping pelos castelos
		areawarp $evcity$ + "_gld", 0, 0, 300, 300, "brasilis", 48, 48;
		dispbottom "Chefes:";
		for(set @i, 1; @i<=5; set @i, @i + 1){
			//Limpando mapa
			areawarp $evcity$ + "g_cas0" + @i, 0, 0, 300, 300, "brasilis", 266, 105;
			killmonsterall $evcity$ + "g_cas0" + @i;
			
			if(@i==5){
				//Chefe
				monster $evcity$ + "g_cas0" + @i, 0, 0, "--ja--", @mobs5[0], 1, "Evento dos Castelos::OnChefe";
				//Criando monstros normais
				for(set @j, 1; @j<getarraysize(@mobs5); set @j, @j + 1)
					areamonster $evcity$ + "g_cas0" + @i, 0, 0, 300, 300, "--ja--", @mobs5[@j], @mobqtde, "Evento dos Castelos::OnMob";
			}else{
				//Fechando mapa
				disablenpc $evcity$ + "g0" + (@i + 1);
				
				//Sorteando/Mostrando Chefe
				setd "@chefe" + @i, rand(0, getarraysize(getd("@mobs" + @i)) + 1);			//Chance do primeiro = 3x maior
				if(!getd("@mobs" + @i + "[" + getd("@chefe" + @i) + "]")) setd "@chefe" + @i, 0;	//Se o chefe não existir, chefe=primeiro
				set @chefeId, getd("@mobs" + @i + "[" + getd("@chefe" + @i) + "]");
				dispbottom " " + @i + ". " + strmobinfo(2, @chefeId) + " (" + @chefeId + ")";
				
				//Escolhendo local
				switch(@i){
					case 1: set .@x, 139; set .@y, 139; break;
					case 2: set .@x, 39; set .@y, 25; break;
					case 3: set .@x, 269; set .@y, 265; break;
					case 4: set .@x, 271; set .@y, 29; break;
				}
				
				//Criando Chefe
				monster $evcity$ + "g_cas0" + @i, .@x, .@y, "--ja--", @chefeId, 1, "Evento dos Castelos::OnChefe";
				
				//Criando MVPs
				for(set @j, 0; @j<getarraysize(getd("@mobs" + @i)); set @j, @j + 1)
					if(@j != getd("@chefe" + @i))
						areamonster $evcity$ + "g_cas0" + @i, 0, 0, 300, 300, "--ja--", getd("@mobs" + @i + "[" + @j + "]"), 1, "Evento dos Castelos::OnMvp";
				//if(@i==1) dispbottom "mobcount('"+$evcity$ + "g_cas0" + $castelo + "', 'Evento dos Castelos::OnMvp') = "+mobcount($evcity$ + "g_cas0" + $castelo, "Evento dos Castelos::OnMvp");
				//Criando monstros normais
				for(set @j, 1; @j<getarraysize(.mobs); set @j, @j + 1)
					areamonster $evcity$ + "g_cas0" + @i, 0, 0, 300, 300, "--ja--", .mobs[@j], @mobqtde, "Evento dos Castelos::OnMob";
			}
			
			//Limpando variáveis
			deletearray getd("@mobs" + @i + "[0]"), getarraysize(getd("@mobs" + @i));
			setd "@chefe" + @i, 0;
			set @chefeId, 0;
		}
		enablenpc "Invasão dos Castelos";
		logmes "[Castelos] Evento iniciado";
		callfunc "EvCastelosWarp";
		announce "[Evento] Olá jogadores do Cortez Online!", bc_all;
		sleep 4000;
		announce "[Evento] Um novo evento acaba de começar", bc_all;
		sleep 4000;
		announce "[Evento] Quatro castelos foram invadidos", bc_all;
		sleep 4000;
		announce "[Evento] E agora precisamos conquistar os castelos novamente", bc_all;
		sleep 4000;
		announce "[Evento] Quem quiser ajudar, estarei esperando no centro da cidade", bc_all;
		end;
		
	OnChefe:
		disablenpc $evcity$ + "g0" + $castelo;
		specialeffect2 573; sleep2 50;
		specialeffect2 581; sleep2 50;
		logmes "[Castelo] O chefe do castelo " + $castelo + " foi morto";
		if($castelo<=4){
			announce "[Evento] O chefe do " + $castelo + "º castelo foi morto", bc_all;
			sleep 2000;
			announce "[Evento] "+ ($castelo==4 ? "Os prêmios se encontram no último castelo" : "O próximo castelo foi aberto") +"!", bc_all;
			sleep 2000;
			killmonsterall $evcity$ + "g_cas0" + $castelo;
			set $castelo, $castelo + 1;
			enablenpc $evcity$ + "g0" + $castelo;
			sleep 6000;
			callfunc "EvCastelosWarp";
		}else{
			announce "[Evento] Parabéns!! O Tesouro Especial foi localizado!", bc_all;
			sleep2 2000;
			announce "[Evento] O campeão é " + strcharinfo(0) + ", que achou o prêmio", bc_all;
			getitem 5210, 1;
			sleep 2000;
			mapannounce $evcity$ + "g_cas0" + 5, "[Evento] Vocês têm 5 minutos para matar os tesouros restantes", bc_map;
			set $castelo, 0;
			disablenpc "Invasão dos Castelos";
			sleep 240000; //4 min
			mapannounce $evcity$ + "g_cas0" + 5, "[Evento] O mapa será fechado em 1 minuto", bc_map;
			sleep 60000; //5 min
			callfunc "EvCastelosWarp";
			goto OnFim;
		}
		end;
	
	OnMvp:
		mapannounce $evcity$ + "g_cas0" + $castelo, "[Evento] Um MVP foi morto... Ainda há " + mobcount($evcity$ + "g_cas0" + $castelo, "Evento dos Castelos::OnMvp") + " vivos!", bc_map;
		specialeffect2 377; sleep2 50;
		specialeffect2 381; sleep 50;
	OnMob:
		if($castelo>0 && $castelo<5)
			areamonster $evcity$ + "g_cas0" + $castelo, 0, 0, 300, 300, "--ja--", .mobs[rand(0, getarraysize(.mobs)-1)], 1, "Evento dos Castelos::OnMob";
		else if($castelo==0 && mobcount($evcity$ + "g_cas05", "Evento dos Castelos::OnMob")<=1)
				callfunc "EvCastelosWarp";
		else
			dispbottom "Parabéns, você abriu um dos tesouros. Tesouros restantes: " + (mobcount($evcity$ + "g_cas05", "Evento dos Castelos::OnMob") - ($castelo==0));
		end;
	
	OnCancelar:
		close2;
		logmes "[Castelos] Evento cancelado";
		announce "[Evento] O evento dos castelos foi cancelado por problemas técnicos", bc_all;
		for(set $castelo, 1; $castelo<=5; set $castelo, $castelo + 1){
			enablenpc $evcity$ + "g0" + $castelo;
			killmonsterall $evcity$ + "g_cas0" + $castelo;
			areawarp $evcity$ + "g_cas0" + $castelo, 0, 0, 300, 300, "brasilis", 266, 105;
		}
		areawarp $evcity$ + "_gld", 0, 0, 300, 300, "brasilis", 266, 105;
		set $castelo, 0;
		goto OnFim;
	
	OnWarp:
		callfunc "EvCastelosWarp";
		if(!$castelo) goto OnFim;
		end;
		
	OnInit:
		set $castelo, 0;
	OnFim:
		set $evcity$, "";
		deletearray .mobs[0], getarraysize(.mobs);
		disablenpc "Invasão dos Castelos";
		end;
}


function	script	EvCastelosWarp	{
	switch($castelo){
		case 0: break;
		case 1: set .@x, 110; set .@y, 228; break;
		case 2: set .@x, 293; set .@y, 135; break;
		case 3: set .@x, 320; set .@y, 312; break;
		case 4: set .@x, 146; set .@y, 174; break;
		case 5: set .@x, 185; set .@y, 273; break;
	}
	if(playerattached())
		warp $evcity$ + "_gld", .@x, .@y;
	else if($castelo>0)
		areawarp $evcity$ + "g_cas0" + ($castelo-1), 0, 0, 300, 300, $evcity$ + "_gld", .@x, .@y;
	else
		areawarp $evcity$ + "g_cas05", 0, 0, 300, 300, "brasilis", 266, 105;
	return;
}