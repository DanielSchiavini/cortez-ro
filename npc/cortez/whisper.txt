
schg_cas03,0,0,0	script	evento	-1,{
	end;
	OnWhisperGlobal:
		if(getgmlevel()>=10){
			if(getstrlen(@whispervar0$)<10 || getstrlen(@whispervar0$)==49){
				announce "Por favor inclua uma descrição do seu evento, de ao menos 10 caracteres e no máximo 50.",bc_self,0xFF3366;
				input @whispervar0$;
			}
			if(getstrlen(@whispervar0$)>=10){
				dispbottom "Evento salvo: [Evento] "+@whispervar0$;
				logmes "[Evento] "+@whispervar0$;
				set @whispervar0$,"";
			}else{
				dispbottom "Log do evento cancelado.";
			}
		}else{
			dispbottom "Você não tem acesso à este comando";
		}
		end;
}

schg_cas03,0,0,0	script	bans	-1,{
	end;
	OnWhisperGlobal:
		if(getgmlevel()>=50){
			if(@whispervar3$==""){
				dispbottom "Formato: Personagem#Tempo#Motivo#Detalhes";
				dispbottom "Para enviar arquivos favor cadastrar pelo site.";
			}else{
				dispbottom "Ainda não implementado";
				//insert into forum.bans (data,char,tempo,motivo,detalhes)
			}
		}else{
			dispbottom "Você não tem acesso à este comando";
		}
		end;
}

schg_cas03,0,0,0	script	kafra	-1,{
	end;
	OnWhisperGlobal:
	if(callfunc("podeusarcmd")){
		if(@whispervar0$=="reset"){
			callfunc("resetStats");
			callfunc("resetSkills");
		}else if(@whispervar0$=="resetstats" || @whispervar0$=="resetstatus"){
			callfunc("resetStats");
		}else if(@whispervar0$=="resetskills"){
			callfunc("resetSkills");
		}else{
			callfunc "F_KafraExpress","Kafra Especial","",2,"schg_cas03",48,48;
		}
	}
	end;
	
	OnInit:
		if(gettime(3)>=12)
			end;
	
	OnClock0000:
		setmapflag "schg_cas03",mf_fireworks;
		end;
		
	OnClock1200:
		removemapflag "schg_cas03",mf_fireworks;
		end;
}

schg_cas03,0,0,0	script	recall	-1,{
	end;
	OnWhisperGlobal:
	if(getgmlevel()>=60){
		if(@whispervar0$!=""){
			set @char_id,getcharid(0,@whispervar0$);
			if(@char_id){
				warpchar "schg_cas03",298,306,@char_id;
				warp "schg_cas03",298,306;
			}else{
				dispbottom "Personagem "+@whispervar0$+" não localizado";
			}
		}
	}
}

schg_cas03,0,0,0	script	prof	-1,{
	end;
	OnWhisperGlobal:
	if(callfunc("podeusarcmd"))
		callfunc "jobmaster";
	end;
}

function	script	podeusarcmd	{
	if(getgmlevel()==0){
		dispbottom "Este comando é apenas para jogadores com conta VIP.";
	//}else if(Hp==0){
	//	dispbottom "Use este comando quando estiver vivo.";
	}else if(getmapxy(.@mapname$,@mapx,@mapy,0) == -1) {
		return 0;
	}else if(compare(.@mapname$,"cas") || compare(.@mapname$,"guild") || compare(.@mapname$,"ordeal") || compare(.@mapname$,"arena") ||
			compare(.@mapname$,"sec") || compare(.@mapname$,"tha") || compare(.@mapname$,"quiz") || .@mapname$=="gl_step"){
		dispbottom "Não é possível usar comandos neste mapa!";
	}else{
		return 1;
	}
	return 0;
}
