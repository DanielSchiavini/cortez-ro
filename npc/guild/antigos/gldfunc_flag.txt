//===== eAthena Script =======================================
//= War of Emperium Guild Flags Function
//===== By: ==================================================
//= jAthena - kalen (1.0)
//= 1.1 by Akaru, ho|yAnge|X, and Valaris
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= eAthena 0.1+; RO Episode 4+
//===== Description: =========================================
//= Flags display messages stating whether or not a guild castle has been captured.
//= Allows the guild members to return to a captured castle.
//======================================
//= Break down of arguments used in the function:
//=   arg(0): name of City.
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp
//=   arg(3): y1 coordinate for warp
//=   arg(4): Teleportable (0/1) 
//===== Additional Comments: =================================
//= v1.2: All Guild flags scripts use this function. [kobra_k88]
//= v1.3: Can now only flag in from outside the castle [Kayla]
//= v1.3a: Implemented k3dt's exploit fix [Vicious]
//============================================================

function	script	F_Flags	{
	set @GID, GetCastleData(getarg(1)+"",1);
	if (@GID == 0){
		mes "[ Leis do Divino Reino Cortez de Rune Midgard ]";
		mes " ";
		mes "1. De acordo com as leis do Reino Cortez de Rune Midgard, esta base não foi tomada por nenhum clã";
		mes " ";
		mes "2. Para conquistar esta base, é necessário derrotar todos os guardiões que estão protegendo a base e eliminar o emperium.";
	}else if (getcharid(2) != @GID || getarg(4) != 1){
		mes "[ Leis do Divino Reino de Rune Midgard Cortez ]";
		mes " ";
		mes "1. De acordo com as leis do Reino Cortez de Rune Midgard, esta base pertence ao clã ^ff0000" + GetGuildName(@GID) + "^000000.";
		mes " ";
		mes "2. O Líder do clã ^ff0000"+GetGuildName(@GID)+"^000000 é ^0000FF" + GetGuildMaster(@GID) + "^000000.";
		mes "Se alguém é contra isto, levante a sua espada para sua própria honra.";
	}else{
		mes "[ Voz Envolvente ]";
		mes "Deseja voltar ao seu ponto de glória?";
		next;
		menu "Voltar!",-,"Não...",L_Sair;
		if (getcharid(2) == GetCastleData(getarg(1)+"",1)) warp getarg(1)+"",getarg(2),getarg(3);
	}
	L_Sair:
	return;
}
