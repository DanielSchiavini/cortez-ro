//===== eAthena Script =======================================
//= War of Emperium Guild Manager Function
//===== By: ==================================================
//= jAthena - kalen (1.0) & eAthena Team
//===== Current Version: =====================================
//= 2.0
//===== Compatible With: =====================================
//= eAthena SVN; RO Episode 4+
//===== Description: =========================================
//= [ Aegis Conversion]
//= The Guild Manager allows the Guildmaster to invest in comerce
//= and defense, hire guardians and kafras, go to the treasure room,
//= and surrender the guild castle.
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, Guardian scripts etc.
//===== Additional Comments: =================================
//= 1.31: Added support for Emsolute Develop [celest]
//= 1.2: All Guild manager scripts use this function. Optimized Comerce and Defense investment. [kobra_k88]
//= 1.2a Function now returns to script that called it. Added disablenpc line to surrender castle option to remove kafra upon surrender.[kobra_k88]
//= 1.2b U can't surrender the base during WOE [Lupus]
//= 1.2c Fixed issue of guardians hp not increasing upon defense investment.[kobra_k88]
//= 1.3 Now you can't install Guardians during WOE [Lupus]
//= 1.4 Remove surrender abbility (Was 100% custom as far as I can tell) [Kayla]
//= 1.41 Fixed possible Economy investment overflow with Emsolute Develop learnt [Lupus]
//= 1.5 Official Novice Castles Menu (u can't invest / hire guardians) [Lupus]
//= 1.6 According to recent info u can re-install Guardians during WoE [Lupus]
//= 1.6a Fix for guild manager recognizing [KarLaeda]
//= 1.6b Fixed the chance for double invest, now 50% instead of 49% [Lupus]
//= 1.7 Changed the names of the Kafra from "Service" to "Staff" [L0ne_W0lf]
//= 1.8 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=	This update changes quite a bit of the internal workings of the guild manager.
//=	No longer uses defined numerical values for guardian HP calculation
//=	Guardian summon display list is now DYNAMIC. Updated with guardian corrections.
//=	Added dialog for castle Abandoning. Commented out be default.
//= 1.9 Fixed guild members being able to access Guild Master services. [L0ne_W0lf]
//= 1.9a Fixed Kafra cutin not closing if you choose not to dismiss her. [L0ne_W0lf]
//= 1.9b Corrected impropper variable being used for defense investment. [L0ne_W0lf]
//= 1.9c Fixed a typo with zeny checkm when installing guardian. [L0ne_W0lf]
//= 2.0 Fixed a typo in abandon function call. [L0ne_W0lf]
//============================================================

function	script	F_GldManager	{
	set .@GID, GetCastleData(getarg(1),1);

	// Define the types of guardians on a per castle basis
	// 1 - Soldier Guardian; 2 - Archer Guardian; 3 - Knight Guardian
	// Aldebaran (Luina)
	if (getarg(1) == "aldeg_cas01") setarray .@guardiantype[0],1,2,2,2,2,3,3,3;
	if (getarg(1) == "aldeg_cas02") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "aldeg_cas03") setarray .@guardiantype[0],3,3,1,1,1,2,2,2;
	if (getarg(1) == "aldeg_cas04") setarray .@guardiantype[0],2,2,2,1,1,1,3,3;
	if (getarg(1) == "aldeg_cas05") setarray .@guardiantype[0],2,2,1,1,3,3,3,3;
	// Geffen (Britoniah)
	if (getarg(1) == "gefg_cas01") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "gefg_cas02") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "gefg_cas03") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	if (getarg(1) == "gefg_cas04") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	if (getarg(1) == "gefg_cas05") setarray .@guardiantype[0],2,2,1,1,1,3,3,3;
	// Payon (Baulder)
	if (getarg(1) == "payg_cas01") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas02") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas03") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas04") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	if (getarg(1) == "payg_cas05") setarray .@guardiantype[0],2,2,2,2,2,2,2,3;
	// Prontera (Valkyrie Realms)
	if (getarg(1) == "prtg_cas01") setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	if (getarg(1) == "prtg_cas02") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "prtg_cas03") setarray .@guardiantype[0],3,3,3,1,1,2,2,2;
	if (getarg(1) == "prtg_cas04") setarray .@guardiantype[0],3,3,3,1,1,1,2,2;
	if (getarg(1) == "prtg_cas05") setarray .@guardiantype[0],3,3,3,1,1,1,2,2;

	if (getgmlevel()>70){
		mes "[ "+getarg(0)+" ]";
		mes "Olá GM. Olhe o status do castelo:";
		mes " ";
		mes " ^0000ffO nível de comércio é " + GetCastleData(getarg(1),2) + ".";
		if (GetCastleData(getarg(1),4) > 0)
			mes "- Já investido" + GetCastleData(getarg(1),4) + " vez hoje.";
		mes "^009900Nosso nível de defesa é " + GetCastleData(getarg(1),3) + ".";
		if (GetCastleData(getarg(1),5) > 0)
			mes " - Já investido " + GetCastleData(getarg(1),5) + " vez hoje.";
		mes " ";
		if (GetCastleData(getarg(1),9) == 1)
			mes " - O castelo tem uma Kafra";
		else
			mes " - O castelo não tem uma Kafra";
		mes " ";
		if (getcharid(2) == .@GID){
			next;
		}else{
			mes "Deseja ir para a sala do mestre?";
			if (select("Cancelar:Ir para a sala.") == 2) {
				close2;
				warp getarg(1),getarg(2),getarg(3);
			}
			return 0;
		}
	}
	if (.@GID == 0){
		mes "[ "+getarg(0)+" ]";
		mes "Estou esperando meu Mestre.";
		return 0;
	}
	if (getcharid(2) != .@GID){
		mes "[ "+getarg(0)+" ]";
		mes "Somente obedeço à ^ff0000" + getguildmaster(.@GID) + "^000000.";
		mes "Onde estão os Guardiões?! Tire este fanfarrão daqui agora mesmo!";
		return 0;
	}
	if (strcharinfo(0) != getguildmaster(.@GID)){
		mes "[ "+getarg(0)+" ]";
		mes "Somente obedeço à ^ff0000" + getguildmaster(.@GID) + "^000000.";
		return 0;
	}
	mes "[ "+getarg(0)+" ]";
	mes "Bem vindo. Meu honorável mestre, ^ff0000" + getguildmaster(.@GID) + "^000000...";
	mes "Seu humilde servo, "+getarg(0)+", está aqui para lhe ajudar.";
	next;

	// To allow abandoning of castles, uncomment the following switch,
	// comment out the switch below it, and uncomment case 7.
	//switch(select("Castle briefing:Invest in commercial growth:Invest in Castle Defenses:Summon Guardian:Employ / discharge a storehouse staff member:Go into Master's room:Abandon Castle")) {
	switch(select("Informações:Investir em Comércio:Investir em Defesa:Instalar Guardião:Instalar Serviço Kafra:Ir para a Sala do Mestre")) {
	case 1:
		mes "[ "+getarg(0)+" ]";
		mes "Meu senhor, aqui estão as informações do castelo.";
		mes " ";
		mes " ^0000ffO nível de comércio é " + GetCastleData(getarg(1),2) + ".";
		if (GetCastleData(getarg(1),4) > 0)
			mes "- Já investido" + GetCastleData(getarg(1),4) + " vez hoje.";
		mes "^009900Nosso nível de defesa é " + GetCastleData(getarg(1),3) + ".";
		if (GetCastleData(getarg(1),5) > 0)
			mes " - Já investido " + GetCastleData(getarg(1),5) + " vez hoje.";
		mes " ";
		mes "^000000É só isso, meu mestre.";
		return 0;

	case 2:
		set .@Economy,GetCastleData(getarg(1),2);
		if(.@Economy < 8) set .@eco_invest,10000;
		if(.@Economy >= 8) set .@eco_invest,20000;
		if(.@Economy >= 16) set .@eco_invest,40000;
		if(.@Economy >= 25) set .@eco_invest,80000;
		if(.@Economy >= 34) set .@eco_invest,160000;
		if(.@Economy >= 44) set .@eco_invest,320000;
		if(.@Economy >= 54) set .@eco_invest,640000;
		if(.@Economy >= 65) set .@eco_invest,1280000;
		if(.@Economy >= 76) set .@eco_invest,2560000;
		if(.@Economy >= 88) set .@eco_invest,5120000;
		mes "[ "+getarg(0)+" ]";
		mes "Se o senhor investir em comércio, a quantidade de tesouros criados pelo castelo irá aumentar. Então, se quiser algum futuro, é necessário investir.";
		mes " ";
		mes "Inicialmente, é possível investir uma vez só, mas se o senhor pagar mais caro, poderá investir uma segunda vez no dia.";
		if (.@Economy == 100) {
			mes " ";
			mes "^ff0000O grau de crescimento do castelo está no máximo, 100%. Sem mais investimentos necessários. É o que eu esperava de um economista como você, meu mestre.^000000";
			return 0;
		}
		mes " ";
		if (GetCastleData(getarg(1),4) == 2) {
			mes "^ff0000Você já investiu duas vezes hoje. Não temos como investir mais.^000000 Estou vendo que as reservas do clã irão crescer rapidamente.";
			return 0;
		}
		else if (GetCastleData(getarg(1),4) == 0)
			mes "O valor necessário para investir agora é ^ff0000" + .@eco_invest + "^000000 zenys. Você irá investir?";
		else
			mes "Você já investiu uma vez. Deseja investir denovo? Mais ^ff0000" + .@eco_invest + "^000000 zenys serão necessários.";
		next;
		if (select("Investir no comércio:Cancelar") == 1) {
			mes "[ "+getarg(0)+" ]";
			if (Zeny < .@eco_invest) {
				mes "Desculpe mas você não tem zeny suficiente para fazer o investimento. Mestre, você falhou como líder.";
				return 0;
			}
			set zeny,zeny-.@eco_invest;
			SetCastleData getarg(1),4,GetCastleData(getarg(1),4)+1;
			SetCastleData getarg(1),2,.@Economy + 1 + (.@Economy<99 && rand(2) && getgdskilllv(.@GID,10014));
			mes "Pronto, já finalizamos o investimento. Espero lhe ver para mais investimentos nos próximos dias, para que o nosso crescimento não pare.";
			return 0;
		}
		mes "[ "+getarg(0)+" ]";
		mes "Irei fazer como você pedir, meu mestre... Sem pressa. Não há nada que não consigamos alcançar.";
		return 0;

	case 3:
		set .@Defence,GetCastleData(getarg(1),3);
		if(.@Defence < 8) set .@def_invest,20000;
		if(.@Defence >= 8) set .@def_invest,40000;
		if(.@Defence >= 16) set .@def_invest,80000;
		if(.@Defence >= 25) set .@def_invest,160000;
		if(.@Defence >= 34) set .@def_invest,320000;
		if(.@Defence >= 44) set .@def_invest,640000;
		if(.@Defence >= 54) set .@def_invest,1280000;
		if(.@Defence >= 65) set .@def_invest,2560000;
		if(.@Defence >= 76) set .@def_invest,5120000;
		if(.@Defence >= 88) set .@def_invest,10240000;
		mes "[ "+getarg(0)+" ]";
		mes "Se você aumentar as defesas do castelo, a durabilidade dos guardiões do emperium vai aumentar. Assim, se você se preocupar nas batalhas que virão, alguns investimentos serão necessários.";
		mes " ";
		mes "Originalmente você pode investir uma vez só, mas se você pagar mais, poderá investir duas.";
		if (.@Defence == 100) {
			mes " ";
			mes "^ff0000Mas o nível de defesa do castelo está no máximo, 100%. Não são necessários mais investimentos. Somente como esperado de um estrategista como o senhor, meu mestre.^000000";
			return 0;
		}
		mes " ";
		if (GetCastleData(getarg(1),5) == 2) {
			mes "^ff0000Você já investiu duas vezes hoje. Não é possível investir mais.^000000 Espero que as defesas do castelo continuem crescendo rápido assim.";
			return 0;
		}
		else if (GetCastleData(getarg(1),5) == 1)
			mes "O valor necessário para investimentos é ^ff0000" + .@def_invest + "^000000 zenys. O senhor irá investir?";
		else
			mes "Já investimos uma vez hoje. Deseja investir novamente? Mais ^ff0000" + .@def_invest + "^000000 zenys serão necessários.";
		next;

		if (select("Investir na defesa.:Cancelar") == 1) {
			mes "[ "+getarg(0)+" ]";
			if (Zeny < .@def_invest) {
				mes "Desculpe, mas o senhor não tem zeny suficiente para investir nas reservas do clã. O senhor falhou, meu mestre.";
				return 0;
			}
			set Zeny,Zeny-.@def_invest;
			SetCastleData getarg(1),5,GetCastleData(getarg(1),5)+1;
			SetCastleData getarg(1),3,.@Defence+1;
			mes "Acabamos de investir no castelo. Espero que voltemos a investir nos próximos dias.";
			return 0;
		}
		mes "[ "+getarg(0)+" ]";
		mes "Irei fazer como o senhor me pediu... Sem pressa. Não há nada que não podemos conseguir.";
		return 0;

	case 4:
		mes "[ "+getarg(0)+" ]";
		mes "Ainda deseja criar um guardião? Será um protetor para nosso castelo, até o fim.";
		mes "Escolha o guardião que quiser.";
		next;

		for( set .@i, 0; .@i <= 7 ; set .@i, .@i+1 )
		{
			if      (.@guardiantype[.@i] == 1) set .@type$,"Guardião Soldado";
			else if (.@guardiantype[.@i] == 2) set .@type$,"Guardião Arqueiro";
			else                               set .@type$,"Guardião Cavaleiro";

			if( guardianinfo(getarg(1),.@i,0) )
				setarray .@name$[.@i], .@type$ + " - Instalado (" + guardianinfo(getarg(1),.@i,2) + "/" + guardianinfo(getarg(1),.@i,1) + ")";
			else
				setarray .@name$[.@i], .@type$ + " - Disponível";
		}
	
		set .@menu$,.@name$[0]+":"+.@name$[1]+":"+.@name$[2]+":"+.@name$[3]+":"+.@name$[4]+":"+.@name$[5]+":"+.@name$[6]+":"+.@name$[7];

		// this thing will propagate outside of this function
		switch(select(.@menu$)) {
		case 1: set @GDnum,10; break;
		case 2: set @GDnum,11; break;
		case 3: set @GDnum,12; break;
		case 4: set @GDnum,13; break;
		case 5: set @GDnum,14; break;
		case 6: set @GDnum,15; break;
		case 7: set @GDnum,16; break;
		case 8: set @GDnum,17; break;
		}

		mes "[ "+getarg(0)+" ]";
		mes "Deseja criar o guardião escolhido? 10,000 zenys serão necessários.";
		next;

		if (select("Criar:Cancelar") == 1) {
			mes "[ "+getarg(0)+" ]";
			if (getgdskilllv(.@GID,10002) == 0) {
				mes "Mestre, não temos materiais para construir guardiões. Se deseja acumular o conhecimento sobre guardiões, precisamos aprender a habilidade do clã primeiro. O guardião não foi criado.";
				return 0;
			}
			if (GetCastleData(getarg(1),@GDnum) == 1) {
				mes "Mestre, este guardião já está em nosso castelo. Não podemos criar mais.";
				return 0;
			}
			if (Zeny < 10000) {
				mes "Bem... Desculpe, mas não temos fundos para criar guardiões. O guardião não foi criado.";
				return 0;
			}
			set zeny,zeny-10000;
			SetCastleData getarg(1),@GDnum,1; // mark as 'installed'
			// guardian will be spawned outside of this function
			mes "Pronto, o guardião foi criado. Agora temos esta grande força para nos ajudar!";
			return 1;

		}
		mes "[ "+getarg(0)+" ]";
		mes "Fiz como o senhor pediu, mas lembre que se o senhor tiver algum zeny, seria melhor usar um guardião.";
		return 0;

	case 5:
		if (GetCastleData(getarg(1),9) == 1) {
			mes "[ "+getarg(0)+" ]";
			mes "Agora estamos contratando uma funcionária... Ou você deseja despedir alguma funcionária contratada?";
			next;
			if(select("Despedir:Cancelar")==1) {
				cutin "kafra_01",2;
				mes "[ Kafra ]";
				mes "Me esforcei tanto... Como podes fazer isso, mestre? Por favor... Por favor, reconsidere... Veja direito o que está fazendo, mestre... Por favor...";
				next;
				if(select("Despedir:Cancelar")==1) {
					mes "[ Kafra ]";
					mes "Oh, meu deus! Não tem sentido...!";
					next;
					cutin "kafra_01",255;
				}else{
					mes "[ Kafra ]";
					mes "Irei trabalhar muito pesado para o senhor!!! Muito Obrigado!";
					close2;
					cutin "kafra_01",255;
					return 0;
				}
			}else{
				mes "[ "+getarg(0)+" ]";
				mes "Ela trabalhou pesado, na minha opinião... Foi uma ótima decisão continuarmos com ela.";
				return 0;
			}
			disablenpc "Serviço Kafra#"+getarg(4);
			SetCastleData getarg(1),9,0;
			mes "[ "+getarg(0)+" ]";
			mes "....";
			mes "Acabamos de despedir a funcionária... Mas... Você se arrepende da decisão?";
			return 0;
		} else {
			mes "[ "+getarg(0)+" ]";
			mes "Devo ligar para o escritório da Kafra para contratar uma funcionária para nós?";
			mes "^ff0000 10,000 zeny é o valor do serviço. ";
			next;
			if (select("Pagar.:Cancelar") == 1) {
				mes "[ "+getarg(0)+" ]";
				if (getgdskilllv(.@GID,10001) == 0) {
					mes "Liguei para a Kafra, mas como ainda não temos contrato com eles não consegui contratá-los. Para conseguir um contrato, é necessário aprender a habilidade do clã.";
					return 0;
				}
				if (Zeny < 10000) {
					mes "[ "+getarg(0)+" ]";
					mes "Desculpe... Desculpe mas não temos zeny suficiente para a Kafra.";
					return 0;
				}
				set Zeny,Zeny-10000;
				enablenpc "Serviço Kafra#"+getarg(4);
				SetCastleData getarg(1),9,1;
				mes "Pronto, eles mandarão uma funcionária para nossa base. Ela chegará em alguns instantes...";
				next;
				next;
				next;
				next;
				cutin "kafra_01",2;
				mes "[ Kafra ]";
				mes "Como vai o senhor? O escritório central pediu para eu vir aqui.";
				mes "Irei trabalhar o quanto puder para ajudar o seu clã.";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				mes "O valor de 10 mil zeny pago é o pagamento mensal da funcionária. No final do mês o senhor precisa pagar a taxa novamente.";
				mes "Assim nossos funcionários ficam felizes.";
				return 0;
			}
			mes "[ "+getarg(0)+" ]";
			mes "Fiz o que o senhor pediu, mas para que os membros do clã trabalhem direito, seria importante contratar uma Kafra agora mesmo.";
			return 0;
		}

	case 6:
		mes "[ "+getarg(0)+" ]";
		mes "Deseja ir para a sala do mestre?";
		mes "O local é somente para você, e ninguém mais pode entrar...";
		next;
		if (select("Ir para a sala.:Cancelar") == 1) {
			mes "[ "+getarg(0)+" ]";
			mes "Irei te mostrar o caminho escondido. Me siga, por favor...";
			mes "Quando desejar voltar aqui, aperte o botão secreto.";
			warp getarg(1),getarg(2),getarg(3);
			end;
		}
		mes "[ "+getarg(0)+" ]";
		mes "Tesouros são produzidos todos os dias... Se o senhor não os abrir em tempo, eles deixarão de ser produzidos.";
		mes "Portanto, é melhor o senhor checar constantemente.";
		return 0;

	//case 7:
	//	mes "[ "+getarg(0)+" ]";
	//	mes "Master!!";
	//	mes "Will you give up this Castle after all that we have all gone through?!";
	//	mes "If.. If this is your final decision, what does all the blood that has been shed mean to you!?";
	//	mes "Please reconsider! Master!!";
	//	next;
	//	if (select("Leave Agit.:Cancel") == 1) {
	//		mes "[ "+getarg(0)+" ]";
	//		mes "Master!! Please think it over, Master!!";
	//		mes "The blood we shed so far...don't you care for all the sacrifices that were made?!";
	//		next;
	//		if (select("Cancel:Leave Castle.") == 1) {
	//			mes "[ "+getarg(0)+" ]";
	//			mes "I knew you were kidding, Master!!";
	//			mes "Please, Master.. Although you were kidding, don't do that again. You really scared me for a moment.";
	//			return 0;
	//		}
	//		mes "[ "+getarg(0)+" ]";
	//		mes "Master!!.....";
	//		mes "...... Ma..s...ter.....";
	//		callfunc "F_GuildBreak",getarg(1),getarg(4);
	//		return 0;
	//	}
	//	mes "[ "+getarg(0)+" ]";
	//	mes "I knew you were kidding, Master!!";
	//	mes "Please, Master.. Although you were kidding, don't do that again. You really scared me for a moment.";
	//	return 0;
	}
}
