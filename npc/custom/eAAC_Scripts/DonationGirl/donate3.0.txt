//===== Athena Script =======================================
//= Donation NPC
//===== By ==================================================
//= Josh
//===== Version =============================================
//= 1.0 - First release. Probably contains bugs/security
//=	  risks
//= 1.1 - Added a check for whether the account exists when
//=	  adding a donator. Need to improve ordering when
//=	  viewing all donations.
//= 1.2 - Modified for public use. Added checkweight feature.
//= 2.0 - Many changes, especially ones I had always wanted
//=	  to add to this script. Includes reading items from
//=	  a separate sql table and more database manipulation
//=	  options for GMs.
//= 2.1 - Made few changes including the add/remove items
//=	  feature.
//= 3.0 - All strings inputed by a user and user/char names
//=	  in sql queries are now escaped. Each item has a
//=	  price rather than a quantity. This script can work
//=	  with decimals.
//===== Compatible With =====================================
//= eAthena - any version that contains the escape_sql
//=	      function (Stable 6299 OR Trunk 6262)
//===== Description =========================================
//= A script that lets a player claim an item for donating.
//= Allows a GM to input each donation.
//===== Comments ============================================
//= This script uses an sql table (donate) to store 
//= variables for the amount donated by a user.
//===== Installation ========================================
//= You must execute donate.sql and donate_item_db.sql before
//= using this script.
//===========================================================
//= Thanks to Vich for helping me with the SQL syntax.
//= Thanks to Lance for helping me with the the arrays and 
//= for implementing query_sql.
//= Thanks to Skotlex for implementing escape_sql.
//===========================================================

prontera,145,179,5	script	Donate	714,{

if (getgmlevel() >= 80) goto L_GM;
L_START:
mes "[Doações]";
mes "Olá eu sou a garota da doação! Se você doou algo pro servidor, pode retirar sua recompensa aqui!";
next;
menu "Mais informações",-,"Retirar prêmio",L_CHECK,"Estatísticas",L_STATS;
L_INFO:
mes "[Doações]";
mes "Todo mês nós gastamos muito dinheiro com o servidor.";
next;
mes "[Doações]";
mes "Dessa forma, você pode nos ajudar doando qualquer quantia.";
next;
mes "[Doações]";
mes "Para mostrar nossa gratidão, iremos te dar alguns prêmios!";
next;
mes "[Doações]";
mes "Alguns itens estão disponíveis aqui! Se ao invés de itens você preferir ter a conta VIP é só falar com o GM Baltazar.";
next;
mes "[Doações]";
mes "Ao adquirir uma conta VIP, você vai poder resetar seu personagem no nível 99!";
next;
mes "[Doações]";
mes "Além disso, vai ganhar mais pontos ao resetar, e vai poder também usar alguns comandos, como @autoloot, @mobinfo, @mobsearch, @storage e @gstorage, por exemplo!";
next;
mes "[Doações]";
mes "O servidor irá melhorar com esse dinheiro, trazendo mais pessoas e menos lag!";
next;
menu "Continuar",L_START,"Sair",-;
close;

L_CHECK:
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+escape_sql(getcharid(3))+"", @amount$;
query_sql "SELECT `claimed` FROM `donate` WHERE `account_id` = "+escape_sql(getcharid(3))+"", @claimed$;
query_sql "SELECT MIN(price) FROM `donate_item_db`", @min$;
query_sql "SELECT '"+@amount$+"' - "+@claimed$+"", @value$;
query_sql "SELECT '"+@value$+"' >= "+@min$+"", @enough;

if(@enough) goto L_CLAIM;
mes "[Doações]";
mes "Desculpe, você não tem nenhuma doação feita.";
mes "Se você doou e ainda não retirou seu prêmio,";
mes "Dê a nós um tempo pra processar sua doação.";
close;

L_CLAIM:
mes "[Doações]";
mes "Muito obrigado por doar!!!";
mes "Você tem $"+@value$+" de crédito!";
mes "Vai querer retirar um item agora?";
next;
menu "Não",-,"Sim",L_YES;
mes "[Doações]";
mes "Tudo bem, você pode retirar os itens a qualquer hora!";
close;

L_YES:
mes "[Doações]";
mes "Ótimo... Vai querer que item?";
next;
query_sql "SELECT name FROM `donate_item_db` order by name ASC",$@name$;
set $@menu$, $@name$[0];
	for(set $@i, 1; $@i < 127; set $@i, $@i + 1){
		set $@menu$, $@menu$ + ":" + $@name$[$@i];
	}

set @menu, (select($@menu$))-1;

query_sql "SELECT ID FROM `donate_item_db` WHERE name = '"+$@name$[@menu]+"'", @id;
query_sql "SELECT price FROM `donate_item_db` WHERE ID = "+@id+"", @price$;
query_sql "SELECT TRUNCATE("+@value$+" / "+@price$+",0)", @max;
query_sql "SELECT '"+@value$+"' div "+@price$+"", @max;

mes "[Doações]";
mes ""+$@name$[@menu]+"s custa $"+@price$+" cada.";
mes "Quantos "+$@name$[@menu]+"s você vai querer?";
mes "Máximo: "+@max+".";
input @quantity;

if(@quantity>@max) {
	mes "[Doações]";
	mes "Desculpe, você não tem dinheiro para pedir "+@quantity+" "+$@name$[@menu]+"s.";
	next;
	goto L_CLAIM;
	}

if(!@quantity) {
	mes "[Doações]";
	mes "Você não vai poder receber 0...!";
	next;
	goto L_CLAIM;
	}

if (checkweight(@id,@quantity) == 0) {
	mes "[Doações]";
	mes "Desculpe-me mas você não aguenta carregar "+@quantity+" "+$@name$[@menu]+"s.";
	next;
	goto L_CLAIM;
	}

query_sql "SELECT '"+@quantity+"' * '"+@price$+"'", @total$;
mes "Certeza que quer receber "+@quantity+" "+$@name$[@menu]+"s por R$"+@total$+"?";
next;
menu "Não",L_CLAIM,"Sim",-;
query_sql "UPDATE `donate` SET `claimed` = `claimed` + "+@total$+" WHERE `account_id` = '"+escape_sql(getcharid(3))+"'";
getitem @id,@quantity;
mes "[Doações]";
mes "Obrigado por doar, esperamos que goste do seu prêmio!";
close;

L_STATS:
mes "[Doações]";
query_sql "SELECT SUM(amount) FROM `donate`", @total$; 
mes "Temos um total de $"+@total$+"";
next;
menu "Mais informações",L_INFO,"Receber",L_CHECK,"Estatísticas",L_STATS;
close;

L_GM:
mes "[GM Menu]";
mes "Hello GM!";
mes "What would you like to do?";
next;
menu "Add/Remove Donation",L_GM2,"Add/Remove Items",L_ITEM,"Test Script",L_START;
close;

L_GM2:
menu "Add a donation",L_DONATE,"Remove a donation",L_REMOVE,"View all donations",L_VIEWALL;
close;

L_ITEM:
menu "Add an item",L_NEWITEM,"Remove an item",L_DELITEM,"View all items",L_ALLITEMS,"Return to main menu",L_GM;
close;


L_NEWITEM:
mes "[GM Menu]";
mes "Please enter the item name:";
input @itemname$;
query_sql "SELECT `id` FROM `item_db` WHERE `name_english` = '"+escape_sql(@itemname$)+"'", @iid;
query_sql "SELECT `id` FROM `donate_item_db` WHERE `name` = '"+escape_sql(@itemname$)+"'", @check;
if(@iid==0) goto L_INONE;
mes "[GM Menu]";
mes "Please enter the cost of each "+@itemname$+":";
input @cost$;
query_sql "SELECT '"+escape_sql(@cost$)+"' = 0", @invalid;
if(@invalid) goto L_ZERO;
query_sql "SELECT CAST('"+escape_sql(@cost$)+"' AS DECIMAL)", @cost$;
mes "[GM Menu]";
mes "You have specified that donators can claim "+@itemname$+"s for $"+@cost$+" each.";
mes "Would you like to continue?";
next;
menu "No",L_ITEM,"Yes",-;
if(@check!=0) goto L_REPLACE;
query_sql "INSERT INTO `donate_item_db` VALUES ('"+@iid+"', '"+escape_sql(@itemname$)+"', '"+@cost$+"')";
mes "[GM Menu]";
mes "Item added successfully!";
next;
menu "Add annother item",L_NEWITEM,"Remove an item",L_DELITEM,"View all items",L_ALLITEMS;
close;

L_REPLACE:
mes "[GM Menu]";
mes "Item "+@itemname$+" already exists in the database.";
mes "Would you like to replace it?";
next;
menu "No",L_ITEM,"Yes",-;
query_sql "REPLACE INTO `donate_item_db` VALUES ('"+@iid+"', '"+@itemname$+"', '"+@cost$+"')";
mes "[GM Menu]";
mes "Item replaced successfully!";
next;
menu "Add annother item",L_NEWITEM,"Remove an item",L_DELITEM,"View all items",L_ALLITEMS;
close;

L_INONE:
mes "[GM Menu]";
mes "Item "+@itemname$+" does not exist.";
next;
goto L_ITEM;

L_DELITEM:
mes "[GM Menu]";
mes "Please enter the item name:";
input @itemname$;
query_sql "SELECT `id` FROM `donate_item_db` WHERE `name` = '"+escape_sql(@itemname$)+"'", @iid;
if(@iid==0) goto L_INONE;
next;
mes "[GM Menu]";
mes "You have specified to delete "+@itemname$+" from the database.";
mes "Would you like to continue?";
next;
menu "No",L_ITEM,"Yes",-;
query_sql "DELETE FROM `donate_item_db` WHERE `id` = '"+@iid+"'";
mes "[GM Menu]";
mes "Item deleted successfully!";
next;
menu "Add an item",L_NEWITEM,"Remove another item",L_DELITEM,"View all items",L_ALLITEMS;
close;

L_ALLITEMS:
mes "[GM Menu]";
query_sql "SELECT `name` FROM `donate_item_db` ORDER BY `name` ASC", @items$;
query_sql "SELECT `price` FROM `donate_item_db` ORDER BY `name` ASC", @itemamount$;
for(set @i, 0; @i < getarraysize(@items$); set @i, @i + 1){
		mes ""+@items$[@i]+" - $"+@itemamount$[@i]+"";
	}
next;
goto L_GM;

L_DONATE:
mes "[GM Menu]";
mes "Please enter the donator's username:";
input @donator$;
query_sql "SELECT `account_id` FROM `login` WHERE `userid` = '"+escape_sql(@donator$)+"'", @aid;
if(@aid==0) goto L_NONE;
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid+"", @donated$;
query_sql "SELECT '"+@donated$+"' > 0", @donated;
switch(@donated) {
	case 0:
		mes ""+@donator$+" has not donated before.";
		break;
	case 1:
		mes ""+@donator$+" has donated $"+@donated+".";
		break;
	}
next;
mes "[GM Menu]";
mes "Please enter the amount donated by "+@donator$+"";
input @donating$;
query_sql "SELECT '"+escape_sql(@donating$)+"' = 0", @invalid;
if(@invalid) goto L_ZERO;
query_sql "SELECT CAST('"+escape_sql(@donating$)+"' AS DECIMAL)", @donating$;
mes "[GM Menu]";
mes "You have specified that "+@donator$+" has donated $"+@donating$+".";
mes "Would you like to continue?";
next;
menu "No",L_GM,"Yes",-;
switch(@donated) {
	case 0:
		query_sql "INSERT INTO `donate` VALUES ('"+@aid+"', '"+@donating$+"', '0')";
		break;
	case 1:
		query_sql "UPDATE `donate` SET `amount` = `amount` + "+@donating$+" WHERE `account_id` = '"+@aid+"'";
		break;
	}
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid+"", @newdonated$;
mes "[GM Menu]";
mes "Donation added successfully!";
mes ""+@donator$+" has donated a total of $"+@newdonated$+"";
next;
goto L_GM;

L_ZERO:
mes "[GM Menu]";
mes "You can't have 0 as an amount!";
next;
goto L_GM;

L_NONE:
mes "[GM Menu]";
mes "Account name "+@donator$+" does not exist.";
next;
goto L_GM;

L_REMOVE:
mes "[GM Menu]";
mes "Please enter the donator's username:";
input @donator$;
query_sql "SELECT `account_id` FROM `login` WHERE `userid` = '"+escape_sql(@donator$)+"'", @aid;
if(@aid==0) goto L_NONE;
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid+"", @donated$;
query_sql "SELECT '"+@donated$+"' > 0", @donated;

if(@donated==0) {
	query_sql "DELETE FROM `donate` WHERE `account_id` = '"+@aid+"'";
	mes ""+@donator$+" is not a donator and has been deleted from the donation database.";
	goto L_GM;
	}

mes ""+@donator$+" has donated $"+@donated$+".";
next;
menu "Deduct an amount from "+@donator$+"",L_MINUS,"Remove "+@donator$+" from the donation database",L_DELETE;
close;

L_MINUS:
mes "[GM Menu]";
mes "Please enter the amount "+@donator$+" is to be deducted by:";
input @deduct$;
query_sql "SELECT '"+escape_sql(@deduct$)+"' = 0", @invalid;
if(@invalid) goto L_ZERO;
query_sql "SELECT CAST('"+escape_sql(@deduct$)+"' AS DECIMAL)", @deduct$;
mes "[GM Menu]";
mes "You have specified that "+@donator$+" is to be deducted by $"+@deduct$+".";
mes "Would you like to continue?";
next;
menu "No",L_GM,"Yes",-;
query_sql "UPDATE `donate` SET `amount` = `amount` - "+@deduct$+" WHERE `account_id` = '"+@aid+"'";
query_sql "SELECT `amount` FROM `donate` WHERE `account_id` = "+@aid+"", @afterdeduct$;
mes "[GM Menu]";
mes "Donation deducted successfully!";
mes ""+@donator$+" has donated a total of $"+@afterdeduct$+"";
next;
goto L_GM;

L_DELETE:
mes "[GM Menu]";
mes "You have specified to remove "+@donator$+" from the donation database.";
mes "Would you like to continue?";
next;
menu "No",L_GM,"Yes",-;
query_sql "DELETE FROM `donate` WHERE `account_id` = '"+@aid+"'";
mes "[GM Menu]";
mes "Donator deleted successfully!";
next;
goto L_GM;

L_VIEWALL:
mes "[GM Menu]";
query_sql "SELECT `account_id` FROM `donate` ORDER BY `amount` DESC", @donatoraid;
query_sql "SELECT `amount` FROM `donate` ORDER BY `amount` DESC", @donatedamount$;
for(set @i, 0; @i < getarraysize(@donatoraid); set @i, @i + 1){
	query_sql "SELECT `userid` FROM `login` WHERE `account_id` = '"+@donatoraid[@i]+"'", @donateruserid$;
	for(set @j, 0; @j < getarraysize(@donateruserid$); set @j, @j + 1){
		mes ""+@donateruserid$[@j]+" - "+@donatedamount$[@i]+"";
	}
}
next;
goto L_GM;

}